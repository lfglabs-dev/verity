/- 
  Compiler.CompilationModel.ParamLoading: ABI parameter decoding helpers

  This module isolates calldata/initcode parameter loading so dispatch and
  constructor assembly do not depend on the full statement compiler body.
-/
import Compiler.CompilationModel.AbiTypeLayout

namespace Compiler.CompilationModel

open Compiler
open Compiler.Yul

def isScalarParamType : ParamType → Bool
  | ParamType.uint256 | ParamType.int256 | ParamType.uint8 | ParamType.uint16
  | ParamType.uintN _ | ParamType.intN _ | ParamType.bytesN _
  | ParamType.address | ParamType.bool | ParamType.bytes32 => true
  | _ => false

private def dynamicArrayElementStrideWords (elemTy : ParamType) : Nat :=
  if isDynamicParamType elemTy then
    1
  else
    max 1 (paramHeadSize elemTy / 32)

/-- Whether the dynamic param shape is length-prefixed in the ABI tail.
    Dynamic arrays (`T[]`), `bytes`, and `string` all begin with a 32-byte
    length word followed by data. Dynamic tuples (structs containing nested
    dynamic members) do not — their offset pointer dereferences directly
    to the first head word of the tuple's encoding. (verity#1839) -/
private def isLengthPrefixedDynamicShape : ParamType → Bool
  | ParamType.bytes | ParamType.string | ParamType.array _ => true
  | _ => false

private def genDynamicParamLoads
    (loadWord : YulExpr → YulExpr) (sizeExpr : YulExpr) (headSize : Nat)
    (baseOffset : Nat) (name : String) (ty : ParamType) (headOffset : Nat) :
    List YulStmt :=
  let offsetLoad := YulStmt.let_ s!"{name}_offset"
    (loadWord (YulExpr.lit headOffset))
  let relativeOffset := YulExpr.ident s!"{name}_offset"
  let absoluteOffsetExpr :=
    if baseOffset == 0 then
      relativeOffset
    else
      YulExpr.call "add" [YulExpr.lit baseOffset, relativeOffset]
  let absoluteOffsetName := s!"{name}_abs_offset"
  let absoluteOffsetLoad := YulStmt.let_ absoluteOffsetName absoluteOffsetExpr
  let absoluteOffset := YulExpr.ident absoluteOffsetName
  let offsetBoundsCheck := YulStmt.if_ (YulExpr.call "lt" [relativeOffset, YulExpr.lit headSize]) [
    YulStmt.exprStmt (YulExpr.call "revert" [YulExpr.lit 0, YulExpr.lit 0])
  ]
  let absoluteBoundsCheck := YulStmt.if_ (YulExpr.call "gt" [
    absoluteOffset,
    YulExpr.call "sub" [sizeExpr, YulExpr.lit 32]
  ]) [
    YulStmt.exprStmt (YulExpr.call "revert" [YulExpr.lit 0, YulExpr.lit 0])
  ]
  let preamble := [offsetLoad, offsetBoundsCheck, absoluteOffsetLoad, absoluteBoundsCheck]
  if isLengthPrefixedDynamicShape ty then
    -- Length-prefixed shape (T[] / bytes / string): the offset points to a
    -- length word, the data starts 32 bytes later.
    let lengthLoad := YulStmt.let_ s!"{name}_length"
      (loadWord absoluteOffset)
    let tailHeadEndName := s!"{name}_tail_head_end"
    let tailHeadEndLoad := YulStmt.let_ tailHeadEndName
      (YulExpr.call "add" [absoluteOffset, YulExpr.lit 32])
    let tailRemainingName := s!"{name}_tail_remaining"
    let tailRemainingLoad := YulStmt.let_ tailRemainingName
      (YulExpr.call "sub" [sizeExpr, YulExpr.ident tailHeadEndName])
    let lengthBoundsCheck :=
      match ty with
      | ParamType.bytes | ParamType.string =>
          [YulStmt.if_ (YulExpr.call "gt" [
              YulExpr.ident s!"{name}_length",
              YulExpr.ident tailRemainingName
            ]) [
              YulStmt.exprStmt (YulExpr.call "revert" [YulExpr.lit 0, YulExpr.lit 0])
            ]]
      | ParamType.array elemTy =>
          let elemWords := dynamicArrayElementStrideWords elemTy
          [YulStmt.if_ (YulExpr.call "gt" [
              YulExpr.ident s!"{name}_length",
              YulExpr.call "div" [YulExpr.ident tailRemainingName, YulExpr.lit (32 * elemWords)]
            ]) [
              YulStmt.exprStmt (YulExpr.call "revert" [YulExpr.lit 0, YulExpr.lit 0])
            ]]
      | _ => []
    let dataOffsetLoad := YulStmt.let_ s!"{name}_data_offset"
      (YulExpr.ident tailHeadEndName)
    preamble ++ [lengthLoad, tailHeadEndLoad, tailRemainingLoad]
      ++ lengthBoundsCheck ++ [dataOffsetLoad]
  else
    -- Dynamic tuple / fixedArray-with-dynamic-head: no length word; the
    -- offset points directly to the first head word of the tuple. Set
    -- `_data_offset` straight to `_abs_offset` so downstream Expr lowerings
    -- read `_data_offset + word_offset * 32` (verity#1839, prerequisite
    -- for verity#1832 macro routing).
    let dataOffsetLoad := YulStmt.let_ s!"{name}_data_offset" absoluteOffset
    preamble ++ [dataOffsetLoad]

def genScalarLoad
    (loadWord : YulExpr → YulExpr) (name : String) (ty : ParamType) (offset : Nat) :
    List YulStmt :=
  let load := loadWord (YulExpr.lit offset)
  match ty with
  | ParamType.uint256 =>
      [YulStmt.let_ name load]
  | ParamType.int256 =>
      [YulStmt.let_ name load]
  | ParamType.uint8 =>
      [YulStmt.let_ name (YulExpr.call "and" [load, YulExpr.lit 255])]
  | ParamType.uint16 =>
      [YulStmt.let_ name (YulExpr.call "and" [load, YulExpr.lit 65535])]
  | ParamType.uintN _ =>
      [YulStmt.let_ name load]
  | ParamType.intN _ =>
      [YulStmt.let_ name load]
  | ParamType.bytesN _ =>
      [YulStmt.let_ name load]
  | ParamType.bytes32 =>
      [YulStmt.let_ name load]
  | ParamType.address =>
      [YulStmt.let_ name (YulExpr.call "and" [
        load,
        YulExpr.hex addressMask
      ])]
  | ParamType.bool =>
      [YulStmt.let_ name (YulExpr.call "iszero" [YulExpr.call "iszero" [load]])]
  | _ => []

def genStaticTypeLoads
    (loadWord : YulExpr → YulExpr) (name : String) (ty : ParamType) (offset : Nat) :
    List YulStmt :=
  match ty with
  | ParamType.uint256 | ParamType.int256 | ParamType.uint8 | ParamType.uint16
  | ParamType.uintN _ | ParamType.intN _ | ParamType.bytesN _
  | ParamType.address | ParamType.bool | ParamType.bytes32 =>
      genScalarLoad loadWord name ty offset
  | ParamType.fixedArray elemTy n =>
      (List.range n).flatMap fun i =>
        genStaticTypeLoads loadWord s!"{name}_{i}" elemTy (offset + i * paramHeadSize elemTy)
  | ParamType.tuple elemTys =>
      let rec go (tys : List ParamType) (idx : Nat) (curOffset : Nat) : List YulStmt :=
        match tys with
        | [] => []
        | elemTy :: rest =>
            let elemName := s!"{name}_{idx}"
            let here := genStaticTypeLoads loadWord elemName elemTy curOffset
            here ++ go rest (idx + 1) (curOffset + paramHeadSize elemTy)
        termination_by sizeOf tys
      go elemTys 0 offset
  | _ => []
termination_by sizeOf ty

-- Generate loading stmts for a single param by type. Recurses on ParamType for newtypeOf unwrapping.
def genSingleParamLoad
    (loadWord : YulExpr → YulExpr) (sizeExpr : YulExpr)
    (headSize : Nat) (baseOffset : Nat) (name : String) (ty : ParamType) (headOffset : Nat) :
    List YulStmt :=
  match ty with
  | ParamType.uint256 | ParamType.int256 | ParamType.uint8 | ParamType.uint16
  | ParamType.uintN _ | ParamType.intN _ | ParamType.bytesN _
  | ParamType.address | ParamType.bool | ParamType.bytes32 =>
    genScalarLoad loadWord name ty headOffset
  | ParamType.tuple elemTypes =>
    if isDynamicParamType ty then
      genDynamicParamLoads loadWord sizeExpr headSize baseOffset name ty headOffset
    else
      genStaticTypeLoads loadWord name (ParamType.tuple elemTypes) headOffset
  | ParamType.array _ =>
    genDynamicParamLoads loadWord sizeExpr headSize baseOffset name ty headOffset
  | ParamType.fixedArray _ n =>
    -- Static fixed arrays are decoded inline recursively (including nested tuple members).
    -- For scalar element arrays we preserve `<name>` as an alias to `<name>_0`.
    if isDynamicParamType ty then
      genDynamicParamLoads loadWord sizeExpr headSize baseOffset name ty headOffset
    else
      let staticLoads := genStaticTypeLoads loadWord name ty headOffset
      if n == 0 then staticLoads else
        let firstAlias := match ty with
          | ParamType.fixedArray elemTy _ =>
              if isScalarParamType elemTy then
                [YulStmt.let_ name (YulExpr.ident s!"{name}_0")]
              else
                []
          | _ => []
        staticLoads ++ firstAlias
  | ParamType.bytes | ParamType.string =>
    genDynamicParamLoads loadWord sizeExpr headSize baseOffset name ty headOffset
  | ParamType.adt _ maxFields =>
    -- ADTs: decode (uint8 tag, uint256 field0, ..., uint256 fieldN) from calldata
    -- Tag word: load first word and mask to uint8
    let tagLoad := [YulStmt.let_ name (YulExpr.call "and" [
      loadWord (YulExpr.lit headOffset), YulExpr.lit 255
    ])]
    -- Field words: load consecutive 32-byte words
    let fieldLoads := (List.range maxFields).map fun i =>
      YulStmt.let_ s!"{name}_f{i}" (loadWord (YulExpr.lit (headOffset + (i + 1) * 32)))
    tagLoad ++ fieldLoads
  | ParamType.newtypeOf _ baseType =>
    -- Newtypes are erased to their base type at Yul level (#1727 Step 3b)
    genSingleParamLoad loadWord sizeExpr headSize baseOffset name baseType headOffset

def genParamLoadBodyFrom
    (loadWord : YulExpr → YulExpr) (sizeExpr : YulExpr)
    (headSize : Nat) (baseOffset : Nat) (params : List Param) (headOffset : Nat) :
    List YulStmt :=
  match params with
  | [] => []
  | param :: rest =>
      let stmts := genSingleParamLoad loadWord sizeExpr headSize baseOffset param.name param.ty headOffset
      stmts ++
        genParamLoadBodyFrom loadWord sizeExpr headSize baseOffset rest
          (headOffset + paramHeadSize param.ty)

def genParamLoadsFrom
    (loadWord : YulExpr → YulExpr) (sizeExpr : YulExpr)
    (headStart : Nat) (baseOffset : Nat) (params : List Param) :
    List YulStmt :=
  let headSize := (params.map (fun p => paramHeadSize p.ty)).foldl (· + ·) 0
  let minInputSizeCheck :=
    YulStmt.if_ (YulExpr.call "lt" [sizeExpr, YulExpr.lit (baseOffset + headSize)]) [
      YulStmt.exprStmt (YulExpr.call "revert" [YulExpr.lit 0, YulExpr.lit 0])
    ]
  minInputSizeCheck :: genParamLoadBodyFrom loadWord sizeExpr headSize baseOffset params headStart

def genParamLoads (params : List Param) : List YulStmt :=
  genParamLoadsFrom (fun pos => YulExpr.call "calldataload" [pos]) (YulExpr.call "calldatasize" []) 4 4 params

def constructorArgAliasNames (params : List Param) : List String :=
  (List.range params.length).map (fun idx => s!"arg{idx}")

def constructorBodyScope (params : List Param) : List String :=
  constructorArgAliasNames params ++ params.map (·.name)

private def constructorArgAliases (params : List Param) : List YulStmt :=
  let rec go (ps : List Param) (idx : Nat) (headOffset : Nat) : List YulStmt :=
    match ps with
    | [] => []
    | param :: rest =>
        let source := if isDynamicParamType param.ty then
          YulExpr.ident s!"{param.name}_offset"
        else
          match param.ty with
          | ParamType.uint256 | ParamType.int256 | ParamType.uint8 | ParamType.uint16 | ParamType.address | ParamType.bool | ParamType.bytes32 =>
              YulExpr.ident param.name
          | _ =>
              YulExpr.call "mload" [YulExpr.lit headOffset]
        let alias := YulStmt.let_ s!"arg{idx}" source
        alias :: go rest (idx + 1) (headOffset + paramHeadSize param.ty)
  go params 0 0

def genConstructorArgLoads (params : List Param) : List YulStmt :=
  if params.isEmpty then []
  else
    let argsOffset := YulExpr.call "add" [
      YulExpr.call "dataoffset" [YulExpr.str "runtime"],
      YulExpr.call "datasize" [YulExpr.str "runtime"]
    ]
    let initcodeArgCopy := [
      YulStmt.let_ "argsOffset" argsOffset,
      YulStmt.let_ "argsSize"
        (YulExpr.call "sub" [YulExpr.call "codesize" [], YulExpr.ident "argsOffset"]),
      YulStmt.exprStmt (YulExpr.call "codecopy" [YulExpr.lit 0, YulExpr.ident "argsOffset", YulExpr.ident "argsSize"])
    ]
    let paramLoads := genParamLoadsFrom (fun pos => YulExpr.call "mload" [pos]) (YulExpr.ident "argsSize") 0 0 params
    initcodeArgCopy ++ paramLoads ++ constructorArgAliases params

end Compiler.CompilationModel
