/- 
  Compiler.CompilationModel.Dispatch: Contract assembly and entrypoint wiring

  This module builds IR functions, constructor code, and whole contracts from
  the lower-level statement/expression compilation helpers.
-/
import Compiler.CompilationModel.Compile
import Compiler.CompilationModel.ParamLoading
import Compiler.CompilationModel.ScopeValidation
import Compiler.CompilationModel.TrustSurface

namespace Compiler.CompilationModel

open Compiler
open Compiler.Yul

/-- Pick a fresh internal return variable name for the given index. -/
def pickFreshInternalRetName (usedNames : List String) (idx : Nat) : String :=
  pickFreshName s!"__ret{idx}" usedNames

/-- Generate fresh internal return variable names for an internal function. -/
def freshInternalRetNames (returns : List ParamType) (usedNames : List String) : List String :=
  let retIndices :=
    returns.zipIdx.flatMap fun (retTy, idx) =>
      match retTy with
      | ParamType.array _ => [idx, idx]
      | _ => [idx]
  let (_, namesRev) := retIndices.foldl
    (fun (acc : List String × List String) idx =>
      let (used, names) := acc
      let fresh := pickFreshInternalRetName used idx
      (fresh :: used, fresh :: names))
    (usedNames, [])
  namesRev.reverse

def internalFunctionYulParamNames (params : List Param) : List String :=
  params.flatMap fun param =>
    match param.ty with
    | ParamType.array _ =>
        [s!"{param.name}_data_offset", s!"{param.name}_length"]
    | ParamType.bytes | ParamType.string =>
        [s!"{param.name}_data_offset", s!"{param.name}_length"]
    | ParamType.fixedArray _ _ =>
        if isDynamicParamType param.ty then
          [s!"{param.name}_data_offset"]
        else
          staticParamBindingNames param.name param.ty
    | ParamType.tuple _ =>
        if isDynamicParamType param.ty then
          [s!"{param.name}_data_offset"]
        else
          staticParamBindingNames param.name param.ty
    | ParamType.newtypeOf _ baseTy =>
        if isDynamicParamType param.ty then
          [s!"{param.name}_data_offset"]
        else
          staticParamBindingNames param.name baseTy
    | _ => [param.name]

-- Compile internal function to a Yul function definition (#181)
def compileInternalFunction (fields : List Field) (events : List EventDef) (errors : List ErrorDef)
    (adtTypes : List AdtTypeDef := []) (spec : FunctionSpec) :
    Except String YulStmt := do
  validateFunctionSpec spec
  let returns ← functionReturns spec
  let paramNames := internalFunctionYulParamNames spec.params
  let usedNames := paramNames ++ collectStmtListBindNames spec.body
  let retNames := freshInternalRetNames returns usedNames
  let bodyStmts ← compileStmtList fields events errors .calldata retNames true
    (paramNames ++ retNames) adtTypes spec.body
  pure (YulStmt.funcDef (internalFunctionYulName spec.name) paramNames retNames bodyStmts)

theorem compileInternalFunction_ok_components
    (fields : List Field) (events : List EventDef) (errors : List ErrorDef)
    (spec : FunctionSpec) (stmt : YulStmt)
    (hcompile : compileInternalFunction fields events errors [] spec = Except.ok stmt) :
    ∃ returns retNames bodyStmts,
      validateFunctionSpec spec = Except.ok () ∧
      functionReturns spec = Except.ok returns ∧
      compileStmtList fields events errors .calldata retNames true
        (internalFunctionYulParamNames spec.params ++ retNames) [] spec.body = Except.ok bodyStmts ∧
      stmt = YulStmt.funcDef
        (internalFunctionYulName spec.name)
        (internalFunctionYulParamNames spec.params)
        retNames
        bodyStmts := by
  unfold compileInternalFunction at hcompile
  cases hvalidate : validateFunctionSpec spec
  · rw [hvalidate] at hcompile
    cases hcompile
  case ok _ =>
    cases hreturns : functionReturns spec
    · rw [hvalidate, hreturns] at hcompile
      cases hcompile
    case ok returns =>
      rw [hvalidate, hreturns] at hcompile
      simp only [bind, Except.bind] at hcompile
      cases hbody :
          compileStmtList fields events errors .calldata
            (freshInternalRetNames returns
              (internalFunctionYulParamNames spec.params ++ collectStmtListBindNames spec.body))
            true
            (internalFunctionYulParamNames spec.params ++
              freshInternalRetNames returns
                (internalFunctionYulParamNames spec.params ++ collectStmtListBindNames spec.body))
            []
            spec.body
      · rw [hbody] at hcompile
        cases hcompile
      case ok bodyStmts =>
        rw [hbody] at hcompile
        simp only [pure, Except.pure, Except.ok.injEq] at hcompile
        refine
          ⟨returns,
            freshInternalRetNames returns
              (internalFunctionYulParamNames spec.params ++ collectStmtListBindNames spec.body),
            bodyStmts,
            ?_⟩
        exact ⟨by simp, by simp,
          by simpa using hbody, by simpa using hcompile.symm⟩

theorem compileInternalFunction_some_ok_of_components
    (fields : List Field) (events : List EventDef) (errors : List ErrorDef)
    (spec : FunctionSpec) (returns : List ParamType) (retNames : List String)
    (bodyStmts : List YulStmt)
    (hvalidate : validateFunctionSpec spec = Except.ok ())
    (hreturns : functionReturns spec = Except.ok returns)
    (hretNames :
      retNames =
        freshInternalRetNames returns
          (internalFunctionYulParamNames spec.params ++ collectStmtListBindNames spec.body))
    (hbody :
      compileStmtList fields events errors .calldata retNames true
        (internalFunctionYulParamNames spec.params ++ retNames) [] spec.body = Except.ok bodyStmts) :
    compileInternalFunction fields events errors [] spec =
      Except.ok
        (YulStmt.funcDef
          (internalFunctionYulName spec.name)
          (internalFunctionYulParamNames spec.params)
          retNames
          bodyStmts) := by
  have hbody' :
      compileStmtList fields events errors .calldata
        (freshInternalRetNames returns
          (internalFunctionYulParamNames spec.params ++ collectStmtListBindNames spec.body))
        true
        (internalFunctionYulParamNames spec.params ++
          freshInternalRetNames returns
            (internalFunctionYulParamNames spec.params ++ collectStmtListBindNames spec.body))
        []
        spec.body = Except.ok bodyStmts := by
    simpa [hretNames] using hbody
  let paramNames := internalFunctionYulParamNames spec.params
  let compiledName := internalFunctionYulName spec.name
  have hmap :
      (YulStmt.funcDef
          compiledName
          paramNames
          (freshInternalRetNames returns (paramNames ++ collectStmtListBindNames spec.body))) <$>
        compileStmtList fields events errors .calldata
          (freshInternalRetNames returns (paramNames ++ collectStmtListBindNames spec.body))
          true
          (paramNames ++ freshInternalRetNames returns (paramNames ++ collectStmtListBindNames spec.body))
          []
          spec.body =
      Except.ok
        (YulStmt.funcDef
          compiledName
          paramNames
          (freshInternalRetNames returns (paramNames ++ collectStmtListBindNames spec.body))
          bodyStmts) := by
    simpa [paramNames, compiledName] using
      congrArg
        (fun compiledBody =>
          Except.map
            (fun compiledStmts =>
              YulStmt.funcDef
                compiledName
                paramNames
                (freshInternalRetNames returns (paramNames ++ collectStmtListBindNames spec.body))
                compiledStmts)
            compiledBody)
        hbody'
  unfold compileInternalFunction
  simp [hvalidate, hreturns]
  simpa [paramNames, compiledName, hretNames] using hmap

-- Compile function spec to IR function
def compileFunctionSpec (fields : List Field) (events : List EventDef) (errors : List ErrorDef)
    (adtTypes : List AdtTypeDef := []) (selector : Nat) (spec : FunctionSpec) :
    Except String IRFunction := do
  validateFunctionSpec spec
  let returns ← functionReturns spec
  let paramLoads := genParamLoads spec.params
  let bodyStmts ← compileStmtList fields events errors .calldata [] false
    (spec.params.map (·.name)) adtTypes spec.body
  let allStmts := paramLoads ++ bodyStmts
  let retType := match returns with
    | [single] => single.toIRType
    | _ => IRType.unit
  return {
    name := spec.name
    selector := selector
    params := spec.params.map Param.toIRParam
    ret := retType
    payable := spec.isPayable
    body := allStmts
  }

private def compileSpecialEntrypoint (fields : List Field) (events : List EventDef)
    (errors : List ErrorDef) (adtTypes : List AdtTypeDef := []) (spec : FunctionSpec) :
    Except String IREntrypoint := do
  let bodyChunks ← compileStmtList fields events errors .calldata [] false [] adtTypes spec.body
  pure {
    payable := spec.isPayable
    body := bodyChunks
  }

def pickUniqueFunctionByName (name : String) (funcs : List FunctionSpec) :
    Except String (Option FunctionSpec) :=
  match funcs.filter (·.name == name) with
  | [] => pure none
  | [single] => pure (some single)
  | _ => throw s!"Compilation error: multiple '{name}' entrypoints are not allowed ({issue586Ref})"

-- Check if contract uses mappings
def usesMapping (fields : List Field) : Bool :=
  fields.any fun f => isMapping fields f.name

-- Compile deploy code (constructor)
-- Note: Don't append datacopy/return here - Codegen.deployCode does that
def compileConstructor (fields : List Field) (events : List EventDef) (errors : List ErrorDef)
    (adtTypes : List AdtTypeDef := []) (ctor : Option ConstructorSpec) :
    Except String (List YulStmt) := do
  match ctor with
  | none => return []
  | some spec =>
    let argLoads := genConstructorArgLoads spec.params
    let bodyChunks ← compileStmtList fields events errors .memory [] false
      (spec.params.map (·.name)) adtTypes spec.body
    return argLoads ++ bodyChunks

-- Main compilation function
-- NOTE: this is the pure core compiler and does *not* verify canonical
-- selector/signature correspondence (it only checks count/duplicates).
-- Use `Compiler.Selector.compileChecked` on caller-provided selector lists.
-- WARNING: Order matters! If selector list is reordered but function list isn't,
-- functions will be mapped to wrong selectors with no runtime error.
private def validateCompileInputsBeforeFieldWriteConflict
    (spec : CompilationModel) : Except String Unit := do
  validateIdentifierShapes spec
  match (collectUsedExternalAssumptions spec).find? (fun ext => ext.linkMode == .external) with
  | some ext =>
      throw s!"Compilation error: {spec.name} uses raw linked-helper lowering for external ABI dependency '{ext.name}'. `linked_as := external` dependencies must lower through an ABI-call ECM such as Compiler.Modules.Calls.withReturn so returndata and revert data are preserved."
  | none =>
      pure ()
  match firstInvalidSlotAliasRange spec.slotAliasRanges with
  | some (idx, range) =>
      throw s!"Compilation error: slotAliasRanges[{idx}] has invalid source interval {range.sourceStart}..{range.sourceEnd} in {spec.name} ({issue623Ref}). slotAliasRanges require sourceStart <= sourceEnd."
  | none =>
      pure ()
  match firstSlotAliasSourceOverlap spec.slotAliasRanges with
  | some (idxA, a, idxB, b) =>
      throw s!"Compilation error: slotAliasRanges[{idxA}]={a.sourceStart}..{a.sourceEnd} and slotAliasRanges[{idxB}]={b.sourceStart}..{b.sourceEnd} overlap in source slots in {spec.name} ({issue623Ref}). Ensure slotAliasRanges source intervals are disjoint."
  | none =>
      pure ()
  match firstUnsupportedInternalDynamicParam spec.functions with
  | some (fnName, paramName, ty) =>
      throw s!"Compilation error: internal function '{fnName}' parameter '{paramName}' has unsupported dynamic type {repr ty} ({issue753Ref}). Internal dynamic ABI lowering currently supports only dynamic arrays with single-word static elements."
  | none =>
      pure ()
  match firstDuplicateFunctionParamName spec.functions with
  | some (fnName, dup) =>
      throw s!"Compilation error: duplicate parameter name '{dup}' in function '{fnName}'"
  | none =>
      pure ()
  for fn in spec.functions do
    match firstDuplicateName (fn.params.flatMap paramBindingNames) with
    | some dup =>
        throw s!"Compilation error: function parameter binding name '{dup}' collides with a compiler-generated parameter local in function '{fn.name}'"
    | none =>
        pure ()
  match firstDuplicateConstructorParamName spec.constructor with
  | some dup =>
      throw s!"Compilation error: duplicate parameter name '{dup}' in constructor"
  | none =>
      pure ()
  match spec.constructor with
  | some ctor =>
      match firstDuplicateName (ctor.params.flatMap paramBindingNames) with
      | some dup =>
          throw s!"Compilation error: constructor parameter binding name '{dup}' collides with a compiler-generated parameter local"
      | none =>
          pure ()
  | none =>
      pure ()
  for fn in spec.functions do
    validateFunctionSpec fn
    validateInteropFunctionSpec fn
    validateSpecialEntrypointSpec fn
    validateEventArgShapesInFunction fn spec.events
    validateCustomErrorArgShapesInFunction fn spec.errors
    validateInternalCallShapesInFunction spec.functions fn
    validateExternalCallTargetsInFunction spec.externals fn
  validateConstructorSpec spec.constructor
  validateInteropConstructorSpec spec.constructor
  validateExternalCallTargetsInConstructor spec.externals spec.constructor
  match spec.constructor with
  | none => pure ()
  | some ctor => do
      ctor.body.forM (validateEventArgShapesInStmt "constructor" ctor.params spec.events)
      ctor.body.forM (validateCustomErrorArgShapesInStmt "constructor" ctor.params spec.errors)
      ctor.body.forM (validateInternalCallShapesInStmt spec.functions "constructor")
  for ext in spec.externals do
    let _ ← externalFunctionReturns ext
    validateInteropExternalSpec ext
  match firstDuplicateName ((spec.functions.filter (fun fn => !fn.isInternal)).map functionSignature) with
  | some dup =>
      throw s!"Compilation error: duplicate function signature '{dup}' in {spec.name}"
  | none =>
      pure ()
  match firstDuplicateName ((spec.functions.filter (·.isInternal)).map (·.name)) with
  | some dup =>
      throw s!"Compilation error: duplicate internal function name '{dup}' in {spec.name}; internal function Yul definitions are keyed by name"
  | none =>
      pure ()
  let externalFunctionNames := (spec.functions.filter (fun fn => !fn.isInternal)).map (·.name)
  match (spec.functions.filter (·.isInternal)).find? (fun fn => externalFunctionNames.contains fn.name) with
  | some fn =>
      throw s!"Compilation error: internal function name '{fn.name}' collides with an external function name in {spec.name}; internal function Yul definitions are keyed by name"
  | none =>
      pure ()
  let functionEffects := inferFunctionEffects spec.functions
  for fn in spec.functions do
    validateFunctionSpecMutability functionEffects fn
  match firstDuplicateName (spec.errors.map (·.name)) with
  | some dup =>
      throw s!"Compilation error: duplicate custom error declaration '{dup}'"
  | none =>
      pure ()
  match firstDuplicateName (spec.fields.map (·.name)) with
  | some dup =>
      throw s!"Compilation error: duplicate field name '{dup}' in {spec.name}"
  | none =>
      pure ()
  match firstInvalidPackedBits spec.fields with
  | some (fieldName, packed) =>
      throw s!"Compilation error: field '{fieldName}' has invalid packedBits offset={packed.offset} width={packed.width} in {spec.name} ({issue623Ref}). Require 0 < width <= 256, offset < 256, and offset + width <= 256."
  | none =>
      pure ()
  match firstMappingPackedBits spec.fields with
  | some fieldName =>
      throw s!"Compilation error: field '{fieldName}' is a mapping and cannot declare packedBits in {spec.name} ({issue623Ref}). Packed subfields are only supported for value-word fields."
  | none =>
      pure ()
  match firstUnsupportedStorageArrayElemType spec.fields with
  | some (fieldName, elemType) =>
      throw s!"Compilation error: field '{fieldName}' uses unsupported storage dynamic array element type {repr elemType} in {spec.name} ({issue1571Ref}). This incremental lowering currently supports only one-storage-word elements (uint256, address, bool, bytes32)."
  | none =>
      pure ()
  firstInvalidStructField spec.fields

def validateCompileInputs (spec : CompilationModel) (selectors : List Nat) : Except String Unit := do
  validateCompileInputsBeforeFieldWriteConflict spec
  let fields := applySlotAliasRanges spec.fields spec.slotAliasRanges
  let externalFns := spec.functions.filter (fun fn => !fn.isInternal && !isInteropEntrypointName fn.name)
  match firstFieldWriteSlotConflict fields with
  | some (slot, existingField, conflictingField) =>
      throw s!"Compilation error: storage slot {slot} has overlapping write ranges for '{existingField}' and '{conflictingField}' in {spec.name} ({issue623Ref}). Ensure full-slot writes are unique and packed bit ranges are disjoint per slot."
  | none =>
      pure ()
  match firstInvalidReservedRange spec.reservedSlotRanges with
  | some (idx, range) =>
      throw s!"Compilation error: reservedSlotRanges[{idx}] has invalid interval {range.start}..{range.end_} in {spec.name} ({issue623Ref}). Reserved slot range start must be <= end."
  | none =>
      pure ()
  match firstReservedRangeOverlap spec.reservedSlotRanges with
  | some (idxA, a, idxB, b) =>
      throw s!"Compilation error: reserved slot ranges reservedSlotRanges[{idxA}]={a.start}..{a.end_} and reservedSlotRanges[{idxB}]={b.start}..{b.end_} overlap in {spec.name} ({issue623Ref}). Ensure reserved ranges are disjoint."
  | none =>
      pure ()
  match firstReservedSlotWriteConflict fields spec.reservedSlotRanges with
  | some (slot, ownerName, rangeIdx, range) =>
      throw s!"Compilation error: field write slot {slot} ('{ownerName}') overlaps reservedSlotRanges[{rangeIdx}]={range.start}..{range.end_} in {spec.name} ({issue623Ref}). Adjust field slot/aliasSlots or reservedSlotRanges."
  | none =>
      pure ()
  match firstDuplicateName (spec.events.map (·.name)) with
  | some dup =>
      throw s!"Compilation error: duplicate event name '{dup}' in {spec.name}"
  | none =>
      pure ()
  for eventDef in spec.events do
    validateEventDef eventDef
  match firstDuplicateEventParamName spec.events with
  | some (evName, dup) =>
      throw s!"Compilation error: duplicate parameter name '{dup}' in event '{evName}'"
  | none =>
      pure ()
  match firstDuplicateName (spec.externals.map (·.name)) with
  | some dup =>
      throw s!"Compilation error: duplicate external declaration '{dup}' in {spec.name}"
  | none =>
      pure ()
  let mappingHelpersRequired := usesMapping fields
  let arrayHelpersRequired := contractUsesPlainArrayElement spec
  let arrayElementWordHelpersRequired := contractUsesArrayElementWord spec
  let paramDynamicHeadWordHelpersRequired := contractUsesParamDynamicHeadWord spec
  let mulDiv512HelpersRequired := contractUsesMulDiv512 spec
  let storageArrayHelpersRequired := contractUsesStorageArrayElement spec
  let dynamicBytesEqHelpersRequired := contractUsesDynamicBytesEq spec
  match firstReservedExternalCollision
      spec mappingHelpersRequired arrayHelpersRequired arrayElementWordHelpersRequired
        paramDynamicHeadWordHelpersRequired
        mulDiv512HelpersRequired
        storageArrayHelpersRequired dynamicBytesEqHelpersRequired with
  | some name =>
      if name.startsWith internalFunctionPrefix then
        throw s!"Compilation error: external declaration '{name}' uses reserved prefix '{internalFunctionPrefix}' ({issue756Ref})."
      else
        throw s!"Compilation error: external declaration '{name}' collides with compiler-generated/reserved symbol '{name}' ({issue756Ref}). Rename the external wrapper."
  | none =>
      pure ()
  for err in spec.errors do
    validateErrorDef err
  if externalFns.length != selectors.length then
    throw s!"Selector count mismatch for {spec.name}: {selectors.length} selectors for {externalFns.length} external functions"
  match firstDuplicateSelector selectors with
  | some dup =>
      let names := selectorNames spec selectors dup
      let nameStr := if names.isEmpty then "<unknown>" else String.intercalate ", " names
      throw s!"Selector collision in {spec.name}: {dup} assigned to {nameStr}"
  | none => pure ()

def compileValidatedCore (spec : CompilationModel) (selectors : List Nat) : Except String IRContract := do
  let fields := applySlotAliasRanges spec.fields spec.slotAliasRanges
  let externalFns := spec.functions.filter (fun fn => !fn.isInternal && !isInteropEntrypointName fn.name)
  let internalFns := spec.functions.filter (·.isInternal)
  let mappingHelpersRequired := usesMapping fields
  let arrayHelpersRequired := contractUsesPlainArrayElement spec
  let arrayElementWordHelpersRequired := contractUsesArrayElementWord spec
  let paramDynamicHeadWordHelpersRequired := contractUsesParamDynamicHeadWord spec
  let mulDiv512HelpersRequired := contractUsesMulDiv512 spec
  let storageArrayHelpersRequired := contractUsesStorageArrayElement spec
  let dynamicBytesEqHelpersRequired := contractUsesDynamicBytesEq spec
  let fallbackSpec ← pickUniqueFunctionByName "fallback" spec.functions
  let receiveSpec ← pickUniqueFunctionByName "receive" spec.functions
  let functions ← (externalFns.zip selectors).mapM fun (fnSpec, sel) =>
    compileFunctionSpec fields spec.events spec.errors spec.adtTypes sel fnSpec
  let internalFuncDefs ← internalFns.mapM (compileInternalFunction fields spec.events spec.errors spec.adtTypes)
  let arrayElementHelpers :=
    (if arrayHelpersRequired then
      [ checkedArrayElementCalldataHelper
      , checkedArrayElementMemoryHelper
      ]
    else
      []) ++
    (if arrayElementWordHelpersRequired then
      [ checkedArrayElementWordCalldataHelper
      , checkedArrayElementWordMemoryHelper
      , checkedArrayElementDynamicWordCalldataHelper
      , checkedArrayElementDynamicWordMemoryHelper
      , checkedArrayElementDynamicDataOffsetCalldataHelper
      , checkedArrayElementDynamicDataOffsetMemoryHelper
      -- verity#1849 G1: dynamic-member length helpers share the
      -- `arrayElementWord` gate because they read the same struct-array
      -- elements and have negligible code size; conservative emission is
      -- correct and avoids a separate predicate.
      , checkedArrayElementDynamicMemberLengthCalldataHelper
      , checkedArrayElementDynamicMemberLengthMemoryHelper
      , checkedArrayElementDynamicMemberDataOffsetCalldataHelper
      , checkedArrayElementDynamicMemberDataOffsetMemoryHelper
      -- verity#1849 G2: dynamic-member element helpers gated the same way.
      , checkedArrayElementDynamicMemberElementCalldataHelper
      , checkedArrayElementDynamicMemberElementMemoryHelper
      ]
    else
      []) ++
    (if paramDynamicHeadWordHelpersRequired then
      [ checkedParamDynamicHeadWordCalldataHelper
      , checkedParamDynamicHeadWordMemoryHelper
      , checkedParamDynamicMemberLengthCalldataHelper
      , checkedParamDynamicMemberLengthMemoryHelper
      , checkedParamDynamicMemberDataOffsetCalldataHelper
      , checkedParamDynamicMemberDataOffsetMemoryHelper
      , checkedParamDynamicMemberElementCalldataHelper
      , checkedParamDynamicMemberElementMemoryHelper
      ]
    else
      []) ++
    -- verity#1761: full-precision multiply-divide. `fullMulDivUpHelper`
    -- calls `fullMulDivHelperName`, so emit the down helper unconditionally
    -- when either is used.
    (if mulDiv512HelpersRequired then
      [ fullMulDivHelper
      , fullMulDivUpHelper
      ]
    else
      [])
  let storageArrayElementHelpers :=
    if storageArrayHelpersRequired then
      [checkedStorageArrayElementHelper]
    else
      []
  let dynamicBytesEqHelpers :=
    if dynamicBytesEqHelpersRequired then
      [dynamicBytesEqCalldataHelper, dynamicBytesEqMemoryHelper]
    else
      []
  let fallbackEntrypoint ← fallbackSpec.mapM (compileSpecialEntrypoint fields spec.events spec.errors spec.adtTypes)
  let receiveEntrypoint ← receiveSpec.mapM (compileSpecialEntrypoint fields spec.events spec.errors spec.adtTypes)
  return {
    name := spec.name
    deploy := (← compileConstructor fields spec.events spec.errors spec.adtTypes spec.constructor)
    constructorPayable := spec.constructor.map (·.isPayable) |>.getD false
    functions := functions
    fallbackEntrypoint := fallbackEntrypoint
    receiveEntrypoint := receiveEntrypoint
    usesMapping := mappingHelpersRequired
    internalFunctions := arrayElementHelpers ++ storageArrayElementHelpers ++ dynamicBytesEqHelpers ++ internalFuncDefs
  }

def compile (spec : CompilationModel) (selectors : List Nat) : Except String IRContract := do
  validateCompileInputs spec selectors
  compileValidatedCore spec selectors

theorem validateCompileInputs_identifierShapes_ok
    {spec : CompilationModel}
    {selectors : List Nat}
    (hvalidate : validateCompileInputs spec selectors = Except.ok ()) :
    validateIdentifierShapes spec = Except.ok () := by
  unfold validateCompileInputs validateCompileInputsBeforeFieldWriteConflict at hvalidate
  cases hshapes : validateIdentifierShapes spec with
  | error err =>
      simp [hshapes] at hvalidate
      cases hvalidate
  | ok _ =>
      simp

theorem validateCompileInputs_firstFieldWriteSlotConflict_eq_none
    {spec : CompilationModel}
    {selectors : List Nat}
    (hvalidate : validateCompileInputs spec selectors = Except.ok ()) :
    firstFieldWriteSlotConflict
        (applySlotAliasRanges spec.fields spec.slotAliasRanges) = none := by
  unfold validateCompileInputs at hvalidate
  cases hprefix : validateCompileInputsBeforeFieldWriteConflict spec with
  | error err =>
      simp [hprefix] at hvalidate
      cases hvalidate
  | ok _ =>
      cases hconflict : firstFieldWriteSlotConflict
          (applySlotAliasRanges spec.fields spec.slotAliasRanges) with
      | some conflict =>
          simp [hprefix, hconflict] at hvalidate
          cases hvalidate
      | none =>
          simp

end Compiler.CompilationModel
