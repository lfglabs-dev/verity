import Compiler.CompilationModel.Types
import Compiler.CompilationModel.AbiHelpers
import Compiler.CompilationModel.AbiEncoding
import Compiler.CompilationModel.DynamicData
import Compiler.CompilationModel.EventAbiHelpers
import Compiler.CompilationModel.IssueRefs
import Compiler.CompilationModel.ValidationHelpers
import Compiler.CompilationModel.ExpressionCompile

namespace Compiler.CompilationModel

open Compiler
open Compiler.Yul

def eventZippedWithSource
    (eventDef : EventDef) (args : List Expr) (compiledArgs : List YulExpr) :
    List (EventParam × Expr × YulExpr) :=
  (eventDef.params.zip args).zip compiledArgs |>.map
    (fun ((p, srcExpr), argExpr) => (p, srcExpr, argExpr))

def eventIndexedArgs
    (zippedWithSource : List (EventParam × Expr × YulExpr)) :
    List (EventParam × Expr × YulExpr) :=
  zippedWithSource.filter (fun (p, _, _) => p.kind == EventParamKind.indexed)

def eventUnindexedArgs
    (zippedWithSource : List (EventParam × Expr × YulExpr)) :
    List (EventParam × Expr × YulExpr) :=
  zippedWithSource.filter (fun (p, _, _) => p.kind == EventParamKind.unindexed)

def eventUnindexedHeadSize
    (unindexed : List (EventParam × Expr × YulExpr)) : Nat :=
  (unindexed.map (fun (p, _, _) => eventHeadWordSize p.ty)).foldl (· + ·) 0

def eventHasUnindexedDynamicData
    (unindexed : List (EventParam × Expr × YulExpr)) : Bool :=
  unindexed.any (fun (p, _, _) => eventIsDynamicType p.ty)

def eventLogFunction (indexedLength : Nat) : String :=
  match indexedLength with
  | 0 => "log1"
  | 1 => "log2"
  | 2 => "log3"
  | _ => "log4"

def eventLogArgs
    (dataSizeExpr : YulExpr) (indexedTopicParts : List (List YulStmt × YulExpr)) :
    List YulExpr :=
  [YulExpr.ident "__evt_ptr", dataSizeExpr, YulExpr.ident "__evt_topic0"] ++
    indexedTopicParts.map (·.2)

structure EventDynamicArraySource where
  lengthExpr : YulExpr
  dataOffsetExpr : YulExpr
  source : DynamicDataSource

def eventDynamicArraySource?
    (fields : List Field) (dynamicSource : DynamicDataSource)
    (internalFunctions : List FunctionSpec := []) :
    Expr → Except String (Option EventDynamicArraySource)
  | Expr.param name =>
      pure (some
        { lengthExpr := YulExpr.ident s!"{name}_length"
          dataOffsetExpr := indexedDynamicBaseOffsetExpr dynamicSource name
          source := dynamicSource })
  | Expr.memoryArrayLength name =>
      pure (some
        { lengthExpr := YulExpr.ident s!"{name}_length"
          dataOffsetExpr := YulExpr.ident s!"{name}_data_offset"
          source := .memory })
  | e@(Expr.paramDynamicMemberLength name wordOffset) => do
      let dataOffsetExpr ← compileExprWithInternals fields dynamicSource internalFunctions
        (Expr.paramDynamicMemberDataOffset name wordOffset)
      let lengthExpr ← compileExprWithInternals fields dynamicSource internalFunctions e
      pure (some { lengthExpr, dataOffsetExpr, source := dynamicSource })
  | e@(Expr.arrayElementDynamicMemberLength name index wordOffset) => do
      let dataOffsetExpr ← compileExprWithInternals fields dynamicSource internalFunctions
        (Expr.arrayElementDynamicMemberDataOffset name index wordOffset)
      let lengthExpr ← compileExprWithInternals fields dynamicSource internalFunctions e
      pure (some { lengthExpr, dataOffsetExpr, source := dynamicSource })
  | _ => pure none

def eventParamScalarCompileSupported (ty : ParamType) : Bool :=
  match ty with
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bool | .bytes32 => true
  | .string | .tuple _ | .array _ | .fixedArray _ _ | .bytes => false
  | .adt _ _ => false
  | .newtypeOf _ baseType => eventParamScalarCompileSupported baseType

def eventDefScalarCompileSupported (eventDef : EventDef) : Bool :=
  eventDef.params.all (fun param => eventParamScalarCompileSupported param.ty) &&
    (eventDef.params.filter (fun param => param.kind == EventParamKind.indexed)).length <= 3

def scalarEventUnindexedStoresFrom
    (unindexed : List (EventParam × Expr × YulExpr)) (headOffset : Nat) :
    List YulStmt :=
  match unindexed with
  | [] => []
  | (p, _, argExpr) :: rest =>
      YulStmt.exprStmt (YulExpr.call "mstore" [
        YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.lit headOffset],
        normalizeEventWord p.ty argExpr
      ]) :: scalarEventUnindexedStoresFrom rest (headOffset + eventHeadWordSize p.ty)

def scalarEventUnindexedStores
    (unindexed : List (EventParam × Expr × YulExpr)) :
    List YulStmt :=
  scalarEventUnindexedStoresFrom unindexed 0

def scalarEventIndexedTopicParts
    (indexed : List (EventParam × Expr × YulExpr)) :
    List (List YulStmt × YulExpr) :=
  indexed.map fun (p, _, argExpr) =>
    ([], normalizeEventWord p.ty argExpr)

def adtEventWordStores (basePtr : YulExpr) (sourceName : String)
    (maxFields : Nat) (tagExpr : YulExpr) : List YulStmt :=
  let tagStore := YulStmt.exprStmt (YulExpr.call "mstore" [
    basePtr,
    normalizeEventWord ParamType.uint8 tagExpr
  ])
  let fieldStores := (List.range maxFields).map fun fieldIdx =>
    YulStmt.exprStmt (YulExpr.call "mstore" [
      YulExpr.call "add" [basePtr, YulExpr.lit ((fieldIdx + 1) * 32)],
      YulExpr.ident s!"{sourceName}_f{fieldIdx}"
    ])
  tagStore :: fieldStores

def compileAdtEventWordStores (eventName : String) (paramName : String)
    (srcExpr : Expr) (argExpr basePtr : YulExpr) (maxFields : Nat) :
    Except String (List YulStmt) :=
  match srcExpr with
  | Expr.param sourceName =>
      pure (adtEventWordStores basePtr sourceName maxFields argExpr)
  | _ =>
      throw s!"Compilation error: ADT event param '{paramName}' in event '{eventName}' currently requires direct ADT parameter reference so payload fields can be encoded ({issue586Ref})."

def compileStaticCompositeEventWordStores
    (dynamicSource : DynamicDataSource) (eventName paramName : String)
    (ty : ParamType) (srcExpr : Expr) (argExpr basePtr : YulExpr) :
    Except String (List YulStmt) :=
  match srcExpr with
  | Expr.param name =>
      let leaves := staticCompositeLeaves name ty
      pure <| leaves.zipIdx.map fun ((leafTy, leafExpr), wordIdx) =>
        YulStmt.exprStmt (YulExpr.call "mstore" [
          YulExpr.call "add" [basePtr, YulExpr.lit (wordIdx * 32)],
          normalizeEventWord leafTy leafExpr
        ])
  | Expr.paramDynamicStaticComposite _ _ =>
      pure <| (staticCompositeLeafTypeOffsets 0 ty).map fun (leafOffset, leafTy) =>
        let loadExpr := dynamicWordLoad dynamicSource (YulExpr.call "add" [
          argExpr,
          YulExpr.lit leafOffset
        ])
        YulStmt.exprStmt (YulExpr.call "mstore" [
          YulExpr.call "add" [basePtr, YulExpr.lit leafOffset],
          normalizeEventWord leafTy loadExpr
        ])
  | _ =>
      throw s!"Compilation error: static composite event param '{paramName}' in event '{eventName}' currently requires direct parameter reference or projected static composite source ({issue586Ref})."

def compileScalarEmitFromCompiledArgs
    (eventDef : EventDef) (args : List Expr) (compiledArgs : List YulExpr) :
    List YulStmt :=
  let zippedWithSource := eventZippedWithSource eventDef args compiledArgs
  let indexed := eventIndexedArgs zippedWithSource
  let unindexed := eventUnindexedArgs zippedWithSource
  let sig := eventSignature eventDef
  let sigBytes := bytesFromString sig
  let freeMemPtr := YulExpr.call "mload" [YulExpr.lit freeMemoryPointer]
  let storePtr := YulStmt.let_ "__evt_ptr" freeMemPtr
  let sigStores := (chunkBytes32 sigBytes).zipIdx.map fun (chunk, idx) =>
    YulStmt.exprStmt (YulExpr.call "mstore" [
      YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.lit (idx * 32)],
      YulExpr.hex (wordFromBytes chunk)
    ])
  let topic0Expr := YulExpr.call "keccak256" [YulExpr.ident "__evt_ptr", YulExpr.lit sigBytes.length]
  let topic0Store := YulStmt.let_ "__evt_topic0" topic0Expr
  let unindexedStores := scalarEventUnindexedStores unindexed
  let indexedTopicParts := scalarEventIndexedTopicParts indexed
  let dataSizeExpr := YulExpr.lit (eventUnindexedHeadSize unindexed)
  let logFn := eventLogFunction indexed.length
  let logArgs := eventLogArgs dataSizeExpr indexedTopicParts
  let logStmt := YulStmt.exprStmt (YulExpr.call logFn logArgs)
  [YulStmt.block ([storePtr] ++ sigStores ++ [topic0Store] ++
    unindexedStores ++ [logStmt])]

def compileEmit (fields : List Field) (events : List EventDef)
    (dynamicSource : DynamicDataSource := .calldata)
    (eventName : String) (args : List Expr) (internalFunctions : List FunctionSpec := []) :
    Except String (List YulStmt) := do
  let eventDef? := events.find? (·.name == eventName)
  let eventDef ←
    match eventDef? with
    | some e => pure e
    | none => throw s!"Compilation error: unknown event '{eventName}'"
  if args.length != eventDef.params.length then
    throw s!"Compilation error: event '{eventName}' expects {eventDef.params.length} args, got {args.length}"
  let compiledArgs ← compileExprListWithInternals fields dynamicSource internalFunctions args
  let zippedWithSource := eventZippedWithSource eventDef args compiledArgs
  let indexed := eventIndexedArgs zippedWithSource
  let unindexed := eventUnindexedArgs zippedWithSource
  if indexed.length > 3 then
    throw s!"Compilation error: event '{eventName}' has {indexed.length} indexed params; max is 3"
  if eventDefScalarCompileSupported eventDef then
    pure (compileScalarEmitFromCompiledArgs eventDef args compiledArgs)
  else
  let sig := eventSignature eventDef
  let sigBytes := bytesFromString sig
  let freeMemPtr := YulExpr.call "mload" [YulExpr.lit freeMemoryPointer]
  let storePtr := YulStmt.let_ "__evt_ptr" freeMemPtr
  let sigStores := (chunkBytes32 sigBytes).zipIdx.map fun (chunk, idx) =>
    YulStmt.exprStmt (YulExpr.call "mstore" [
      YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.lit (idx * 32)],
      YulExpr.hex (wordFromBytes chunk)
    ])
  let topic0Expr := YulExpr.call "keccak256" [YulExpr.ident "__evt_ptr", YulExpr.lit sigBytes.length]
  let topic0Store := YulStmt.let_ "__evt_topic0" topic0Expr
  let unindexedHeadSize := eventUnindexedHeadSize unindexed
  let hasUnindexedDynamicData := eventHasUnindexedDynamicData unindexed
  let unindexedTailInit :=
    if hasUnindexedDynamicData then
      [YulStmt.let_ "__evt_data_tail" (YulExpr.lit unindexedHeadSize)]
    else
      []
  let rec compileUnindexedStores
      (remaining : List (EventParam × Expr × YulExpr)) (argIdx : Nat) (headOffset : Nat) :
      Except String (List YulStmt) := do
    match remaining with
    | [] => pure []
    | (p, srcExpr, argExpr) :: rest =>
        let curHeadPtr := YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.lit headOffset]
        let current ←
          match p.ty with
          | ParamType.bytes | ParamType.string =>
              match srcExpr with
              | Expr.param name =>
                  let lenName := s!"__evt_arg{argIdx}_len"
                  let dstName := s!"__evt_arg{argIdx}_dst"
                  let paddedName := s!"__evt_arg{argIdx}_padded"
                  pure ([
                    YulStmt.exprStmt (YulExpr.call "mstore" [curHeadPtr, YulExpr.ident "__evt_data_tail"]),
                    YulStmt.let_ lenName (YulExpr.ident s!"{name}_length"),
                    YulStmt.let_ dstName (YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.ident "__evt_data_tail"]),
                    YulStmt.exprStmt (YulExpr.call "mstore" [YulExpr.ident dstName, YulExpr.ident lenName]),
                  ] ++ dynamicCopyData dynamicSource
                    (YulExpr.call "add" [YulExpr.ident dstName, YulExpr.lit 32])
                    (YulExpr.ident s!"{name}_data_offset")
                    (YulExpr.ident lenName) ++ [
                    YulStmt.let_ paddedName (YulExpr.call "and" [
                      YulExpr.call "add" [YulExpr.ident lenName, YulExpr.lit 31],
                      YulExpr.call "not" [YulExpr.lit 31]
                    ]),
                    YulStmt.exprStmt (YulExpr.call "mstore" [
                      YulExpr.call "add" [
                        YulExpr.call "add" [YulExpr.ident dstName, YulExpr.lit 32],
                        YulExpr.ident lenName
                      ],
                      YulExpr.lit 0
                    ]),
                    YulStmt.assign "__evt_data_tail" (YulExpr.call "add" [
                      YulExpr.ident "__evt_data_tail",
                      YulExpr.call "add" [YulExpr.lit 32, YulExpr.ident paddedName]
                    ])
                  ])
              | _ =>
                  throw s!"Compilation error: unindexed dynamic-bytes event param '{p.name}' in event '{eventName}' currently requires direct bytes/string parameter reference ({issue586Ref})."
          | ParamType.array elemTy =>
              if elemTy == ParamType.bytes || elemTy == ParamType.string then
                  match srcExpr with
                  | Expr.param name =>
                      let lenName := s!"__evt_arg{argIdx}_len"
                      let dstName := s!"__evt_arg{argIdx}_dst"
                      let headLenName := s!"__evt_arg{argIdx}_head_len"
                      let tailLenName := s!"__evt_arg{argIdx}_tail_len"
                      let loopIndexName := s!"__evt_arg{argIdx}_i"
                      let elemOffsetName := s!"__evt_arg{argIdx}_elem_offset"
                      let elemLenPosName := s!"__evt_arg{argIdx}_elem_len_pos"
                      let elemLenName := s!"__evt_arg{argIdx}_elem_len"
                      let elemDataName := s!"__evt_arg{argIdx}_elem_data"
                      let elemDstName := s!"__evt_arg{argIdx}_elem_dst"
                      let elemPaddedName := s!"__evt_arg{argIdx}_elem_padded"
                      pure ([
                        YulStmt.exprStmt (YulExpr.call "mstore" [curHeadPtr, YulExpr.ident "__evt_data_tail"]),
                        YulStmt.let_ lenName (YulExpr.ident s!"{name}_length"),
                        YulStmt.let_ dstName (YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.ident "__evt_data_tail"]),
                        YulStmt.exprStmt (YulExpr.call "mstore" [YulExpr.ident dstName, YulExpr.ident lenName]),
                        YulStmt.let_ headLenName (YulExpr.call "mul" [YulExpr.ident lenName, YulExpr.lit 32]),
                        YulStmt.let_ tailLenName (YulExpr.ident headLenName),
                        YulStmt.for_
                          [YulStmt.let_ loopIndexName (YulExpr.lit 0)]
                          (YulExpr.call "lt" [YulExpr.ident loopIndexName, YulExpr.ident lenName])
                          [YulStmt.assign loopIndexName (YulExpr.call "add" [YulExpr.ident loopIndexName, YulExpr.lit 1])]
                          ([
                            YulStmt.let_ elemOffsetName (dynamicWordLoad dynamicSource (YulExpr.call "add" [
                              YulExpr.ident s!"{name}_data_offset",
                              YulExpr.call "mul" [YulExpr.ident loopIndexName, YulExpr.lit 32]
                            ])),
                            YulStmt.let_ elemLenPosName (YulExpr.call "add" [
                              YulExpr.ident s!"{name}_data_offset",
                              YulExpr.ident elemOffsetName
                            ]),
                            YulStmt.let_ elemLenName (dynamicWordLoad dynamicSource (YulExpr.ident elemLenPosName)),
                            YulStmt.let_ elemDataName (YulExpr.call "add" [YulExpr.ident elemLenPosName, YulExpr.lit 32]),
                            YulStmt.exprStmt (YulExpr.call "mstore" [
                              YulExpr.call "add" [
                                YulExpr.call "add" [YulExpr.ident dstName, YulExpr.lit 32],
                                YulExpr.call "mul" [YulExpr.ident loopIndexName, YulExpr.lit 32]
                              ],
                              YulExpr.ident tailLenName
                            ]),
                            YulStmt.let_ elemDstName (YulExpr.call "add" [
                              YulExpr.call "add" [YulExpr.ident dstName, YulExpr.lit 32],
                              YulExpr.ident tailLenName
                            ]),
                            YulStmt.exprStmt (YulExpr.call "mstore" [
                              YulExpr.ident elemDstName,
                              YulExpr.ident elemLenName
                            ])
                          ] ++ dynamicCopyData dynamicSource
                            (YulExpr.call "add" [YulExpr.ident elemDstName, YulExpr.lit 32])
                            (YulExpr.ident elemDataName)
                            (YulExpr.ident elemLenName) ++ [
                            YulStmt.let_ elemPaddedName (YulExpr.call "and" [
                              YulExpr.call "add" [YulExpr.ident elemLenName, YulExpr.lit 31],
                              YulExpr.call "not" [YulExpr.lit 31]
                            ]),
                            YulStmt.exprStmt (YulExpr.call "mstore" [
                              YulExpr.call "add" [
                                YulExpr.call "add" [YulExpr.ident elemDstName, YulExpr.lit 32],
                                YulExpr.ident elemLenName
                              ],
                              YulExpr.lit 0
                            ]),
                            YulStmt.assign tailLenName (YulExpr.call "add" [
                              YulExpr.ident tailLenName,
                              YulExpr.call "add" [YulExpr.lit 32, YulExpr.ident elemPaddedName]
                            ])
                          ]),
                        YulStmt.assign "__evt_data_tail" (YulExpr.call "add" [
                          YulExpr.ident "__evt_data_tail",
                          YulExpr.call "add" [YulExpr.lit 32, YulExpr.ident tailLenName]
                        ])
                      ])
                  | _ =>
                      throw s!"Compilation error: unindexed dynamic array event param '{p.name}' in event '{eventName}' currently requires direct parameter reference ({issue586Ref})."
              else if indexedDynamicArrayElemSupported elemTy then
                match ← eventDynamicArraySource? fields dynamicSource internalFunctions srcExpr with
                | some source =>
                    let lenName := s!"__evt_arg{argIdx}_len"
                    let dstName := s!"__evt_arg{argIdx}_dst"
                    let elemWordSize := eventHeadWordSize elemTy
                    let byteLenName := s!"__evt_arg{argIdx}_byte_len"
                    let paddedName := s!"__evt_arg{argIdx}_padded"
                    let elemBaseName := s!"__evt_arg{argIdx}_elem_base"
                    let elemOutBaseName := s!"__evt_arg{argIdx}_out_base"
                    let loopIndexName := s!"__evt_arg{argIdx}_i"
                    let leafStores :=
                      (staticCompositeLeafTypeOffsets 0 elemTy).map fun (leafOffset, leafTy) =>
                        let loadExpr := dynamicWordLoad source.source (YulExpr.call "add" [
                          YulExpr.ident elemBaseName,
                          YulExpr.lit leafOffset
                        ])
                        YulStmt.exprStmt (YulExpr.call "mstore" [
                          YulExpr.call "add" [YulExpr.ident elemOutBaseName, YulExpr.lit leafOffset],
                          normalizeEventWord leafTy loadExpr
                        ])
                    pure ([
                      YulStmt.exprStmt (YulExpr.call "mstore" [curHeadPtr, YulExpr.ident "__evt_data_tail"]),
                      YulStmt.let_ lenName source.lengthExpr,
                      YulStmt.let_ dstName (YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.ident "__evt_data_tail"]),
                      YulStmt.exprStmt (YulExpr.call "mstore" [YulExpr.ident dstName, YulExpr.ident lenName]),
                      YulStmt.let_ byteLenName (YulExpr.call "mul" [YulExpr.ident lenName, YulExpr.lit elemWordSize]),
                      YulStmt.for_
                        [YulStmt.let_ loopIndexName (YulExpr.lit 0)]
                        (YulExpr.call "lt" [YulExpr.ident loopIndexName, YulExpr.ident lenName])
                        [YulStmt.assign loopIndexName (YulExpr.call "add" [YulExpr.ident loopIndexName, YulExpr.lit 1])]
                        ([
                          YulStmt.let_ elemBaseName (YulExpr.call "add" [
                            source.dataOffsetExpr,
                            YulExpr.call "mul" [YulExpr.ident loopIndexName, YulExpr.lit elemWordSize]
                          ]),
                          YulStmt.let_ elemOutBaseName (YulExpr.call "add" [
                            YulExpr.call "add" [YulExpr.ident dstName, YulExpr.lit 32],
                            YulExpr.call "mul" [YulExpr.ident loopIndexName, YulExpr.lit elemWordSize]
                          ])
                        ] ++ leafStores),
                      YulStmt.let_ paddedName (YulExpr.call "and" [
                        YulExpr.call "add" [YulExpr.ident byteLenName, YulExpr.lit 31],
                        YulExpr.call "not" [YulExpr.lit 31]
                      ]),
                      YulStmt.exprStmt (YulExpr.call "mstore" [
                        YulExpr.call "add" [
                          YulExpr.call "add" [YulExpr.ident dstName, YulExpr.lit 32],
                          YulExpr.ident byteLenName
                        ],
                        YulExpr.lit 0
                      ]),
                      YulStmt.assign "__evt_data_tail" (YulExpr.call "add" [
                        YulExpr.ident "__evt_data_tail",
                        YulExpr.call "add" [YulExpr.lit 32, YulExpr.ident paddedName]
                      ])
                    ])
                | none =>
                    throw s!"Compilation error: unindexed dynamic array event param '{p.name}' in event '{eventName}' currently requires a direct or projected dynamic array reference ({issue586Ref})."
              else if eventIsDynamicType elemTy then
                match srcExpr with
                | Expr.param name =>
                    let dstName := s!"__evt_arg{argIdx}_dst"
                    let srcBase := indexedDynamicBaseOffsetExpr dynamicSource name
                    let (encStmts, encLen) ←
                      compileUnindexedAbiEncode dynamicSource p.ty srcBase (YulExpr.ident dstName) s!"__evt_arg{argIdx}"
                    pure ([
                      YulStmt.exprStmt (YulExpr.call "mstore" [curHeadPtr, YulExpr.ident "__evt_data_tail"]),
                      YulStmt.let_ dstName (YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.ident "__evt_data_tail"])
                    ] ++ encStmts ++ [
                      YulStmt.assign "__evt_data_tail" (YulExpr.call "add" [YulExpr.ident "__evt_data_tail", encLen])
                    ])
                | _ =>
                    throw s!"Compilation error: unindexed dynamic array event param '{p.name}' in event '{eventName}' currently requires direct parameter reference ({issue586Ref})."
              else
                throw s!"Compilation error: unindexed array event param '{p.name}' in event '{eventName}' has unsupported element type {repr elemTy} ({issue586Ref})."
          | ParamType.fixedArray _ _ | ParamType.tuple _ =>
              if eventIsDynamicType p.ty then
                match srcExpr with
                | Expr.param name =>
                    let dstName := s!"__evt_arg{argIdx}_dst"
                    let srcBase := indexedDynamicBaseOffsetExpr dynamicSource name
                    let (encStmts, encLen) ←
                      compileUnindexedAbiEncode dynamicSource p.ty srcBase (YulExpr.ident dstName) s!"__evt_arg{argIdx}"
                    pure ([
                      YulStmt.exprStmt (YulExpr.call "mstore" [curHeadPtr, YulExpr.ident "__evt_data_tail"]),
                      YulStmt.let_ dstName (YulExpr.call "add" [YulExpr.ident "__evt_ptr", YulExpr.ident "__evt_data_tail"])
                    ] ++ encStmts ++ [
                      YulStmt.assign "__evt_data_tail" (YulExpr.call "add" [YulExpr.ident "__evt_data_tail", encLen])
                    ])
                | _ =>
                    throw s!"Compilation error: unindexed dynamic composite event param '{p.name}' in event '{eventName}' currently requires direct parameter reference ({issue586Ref})."
              else
                compileStaticCompositeEventWordStores dynamicSource eventName p.name p.ty srcExpr argExpr curHeadPtr
          | ParamType.adt _ maxFields =>
              compileAdtEventWordStores eventName p.name srcExpr argExpr curHeadPtr maxFields
          | _ =>
              pure [YulStmt.exprStmt (YulExpr.call "mstore" [curHeadPtr, normalizeEventWord p.ty argExpr])]
        let tail ← compileUnindexedStores rest (argIdx + 1) (headOffset + eventHeadWordSize p.ty)
        pure (current ++ tail)
  let unindexedStores ← compileUnindexedStores unindexed 0 0
  let dataSizeExpr :=
    if hasUnindexedDynamicData then YulExpr.ident "__evt_data_tail" else YulExpr.lit unindexedHeadSize
  let indexedTopicParts ← indexed.zipIdx.mapM fun ((p, srcExpr, argExpr), idx) => do
    match p.ty with
    | ParamType.bytes | ParamType.string =>
        match srcExpr with
        | Expr.param name =>
            let topicName := s!"__evt_topic{idx + 1}"
            let hashStmts :=
              dynamicCopyData dynamicSource
                (YulExpr.ident "__evt_ptr")
                (YulExpr.ident s!"{name}_data_offset")
                (YulExpr.ident s!"{name}_length") ++ [
              YulStmt.let_ topicName (YulExpr.call "keccak256" [
                YulExpr.ident "__evt_ptr",
                YulExpr.ident s!"{name}_length"
              ])
            ]
            pure (hashStmts, YulExpr.ident topicName)
        | _ =>
            throw s!"Compilation error: indexed dynamic-bytes event param '{p.name}' in event '{eventName}' currently requires direct bytes/string parameter reference ({issue586Ref})."
    | ParamType.array elemTy =>
        match elemTy with
        | ParamType.bytes | ParamType.string =>
            match srcExpr with
            | Expr.param name =>
                let topicName := s!"__evt_topic{idx + 1}"
                let tailLenName := s!"__evt_topic_arg{idx}_tail_len"
                let loopIndexName := s!"__evt_topic_arg{idx}_i"
                let elemOffsetName := s!"__evt_topic_arg{idx}_elem_offset"
                let elemLenPosName := s!"__evt_topic_arg{idx}_elem_len_pos"
                let elemLenName := s!"__evt_topic_arg{idx}_elem_len"
                let elemDataName := s!"__evt_topic_arg{idx}_elem_data"
                let elemDstName := s!"__evt_topic_arg{idx}_elem_dst"
                let elemPaddedName := s!"__evt_topic_arg{idx}_elem_padded"
                let hashStmts := [
                  YulStmt.let_ tailLenName (YulExpr.lit 0),
                  YulStmt.for_
                    [YulStmt.let_ loopIndexName (YulExpr.lit 0)]
                    (YulExpr.call "lt" [YulExpr.ident loopIndexName, YulExpr.ident s!"{name}_length"])
                    [YulStmt.assign loopIndexName (YulExpr.call "add" [YulExpr.ident loopIndexName, YulExpr.lit 1])]
                    ([
                      YulStmt.let_ elemOffsetName (dynamicWordLoad dynamicSource (YulExpr.call "add" [
                        YulExpr.ident s!"{name}_data_offset",
                        YulExpr.call "mul" [YulExpr.ident loopIndexName, YulExpr.lit 32]
                      ])),
                      YulStmt.let_ elemLenPosName (YulExpr.call "add" [
                        YulExpr.ident s!"{name}_data_offset",
                        YulExpr.ident elemOffsetName
                      ]),
                      YulStmt.let_ elemLenName (dynamicWordLoad dynamicSource (YulExpr.ident elemLenPosName)),
                      YulStmt.let_ elemDataName (YulExpr.call "add" [YulExpr.ident elemLenPosName, YulExpr.lit 32]),
                      YulStmt.let_ elemDstName (YulExpr.call "add" [
                        YulExpr.ident "__evt_ptr",
                        YulExpr.ident tailLenName
                      ])
                    ] ++ dynamicCopyData dynamicSource
                      (YulExpr.ident elemDstName)
                      (YulExpr.ident elemDataName)
                      (YulExpr.ident elemLenName) ++ [
                      YulStmt.let_ elemPaddedName (YulExpr.call "and" [
                        YulExpr.call "add" [YulExpr.ident elemLenName, YulExpr.lit 31],
                        YulExpr.call "not" [YulExpr.lit 31]
                      ]),
                      YulStmt.exprStmt (YulExpr.call "mstore" [
                        YulExpr.call "add" [YulExpr.ident elemDstName, YulExpr.ident elemLenName],
                        YulExpr.lit 0
                      ]),
                      YulStmt.assign tailLenName (YulExpr.call "add" [
                        YulExpr.ident tailLenName,
                        YulExpr.ident elemPaddedName
                      ])
                    ]),
                  YulStmt.let_ topicName (YulExpr.call "keccak256" [
                    YulExpr.ident "__evt_ptr",
                    YulExpr.ident tailLenName
                  ])
                ]
                pure (hashStmts, YulExpr.ident topicName)
            | _ =>
                throw s!"Compilation error: indexed dynamic array event param '{p.name}' in event '{eventName}' currently requires direct parameter reference ({issue586Ref})."
        | _ =>
            if indexedDynamicArrayElemSupported elemTy then
              match ← eventDynamicArraySource? fields dynamicSource internalFunctions srcExpr with
              | some source =>
                  let topicName := s!"__evt_topic{idx + 1}"
                  let byteLenName := s!"__evt_arg{idx}_byte_len"
                  let elemWordSize := eventHeadWordSize elemTy
                  let elemBaseName := s!"__evt_arg{idx}_elem_base"
                  let elemOutBaseName := s!"__evt_arg{idx}_out_base"
                  let loopIndexName := s!"__evt_arg{idx}_i"
                  let leafStores :=
                    (staticCompositeLeafTypeOffsets 0 elemTy).map fun (leafOffset, leafTy) =>
                      let loadExpr := dynamicWordLoad source.source (YulExpr.call "add" [
                        YulExpr.ident elemBaseName,
                        YulExpr.lit leafOffset
                      ])
                      YulStmt.exprStmt (YulExpr.call "mstore" [
                        YulExpr.call "add" [
                          YulExpr.ident elemOutBaseName,
                          YulExpr.lit leafOffset
                        ],
                        normalizeEventWord leafTy loadExpr
                      ])
                  let hashStmts := [
                    YulStmt.let_ byteLenName (YulExpr.call "mul" [
                      source.lengthExpr,
                      YulExpr.lit elemWordSize
                    ]),
                    YulStmt.for_
                      [YulStmt.let_ loopIndexName (YulExpr.lit 0)]
                      (YulExpr.call "lt" [YulExpr.ident loopIndexName, source.lengthExpr])
                      [YulStmt.assign loopIndexName (YulExpr.call "add" [YulExpr.ident loopIndexName, YulExpr.lit 1])]
                      ([
                        YulStmt.let_ elemBaseName (YulExpr.call "add" [
                          source.dataOffsetExpr,
                          YulExpr.call "mul" [YulExpr.ident loopIndexName, YulExpr.lit elemWordSize]
                        ]),
                        YulStmt.let_ elemOutBaseName (YulExpr.call "add" [
                          YulExpr.ident "__evt_ptr",
                          YulExpr.call "mul" [YulExpr.ident loopIndexName, YulExpr.lit elemWordSize]
                        ])
                      ] ++ leafStores),
                    YulStmt.let_ topicName (YulExpr.call "keccak256" [
                      YulExpr.ident "__evt_ptr",
                      YulExpr.ident byteLenName
                    ])
                  ]
                  pure (hashStmts, YulExpr.ident topicName)
              | none =>
                  throw s!"Compilation error: indexed dynamic array event param '{p.name}' in event '{eventName}' currently requires a direct or projected dynamic array reference ({issue586Ref})."
            else if eventIsDynamicType elemTy then
              match srcExpr with
              | Expr.param name =>
                  let topicName := s!"__evt_topic{idx + 1}"
                  let srcBase := indexedDynamicBaseOffsetExpr dynamicSource name
                  let (encodeStmts, encodeLen) ←
                    compileIndexedInPlaceEncoding dynamicSource p.ty srcBase (YulExpr.ident "__evt_ptr") s!"__evt_arg{idx}_indexed"
                  pure (encodeStmts ++ [YulStmt.let_ topicName (YulExpr.call "keccak256" [
                    YulExpr.ident "__evt_ptr",
                    encodeLen
                  ])], YulExpr.ident topicName)
              | _ =>
                  throw s!"Compilation error: indexed dynamic array event param '{p.name}' in event '{eventName}' currently requires direct parameter reference ({issue586Ref})."
            else
              throw s!"Compilation error: indexed array event param '{p.name}' in event '{eventName}' has unsupported element type {repr elemTy} ({issue586Ref})."
    | ParamType.fixedArray _ _ | ParamType.tuple _ =>
        if eventIsDynamicType p.ty then
          match srcExpr with
          | Expr.param name =>
              let topicName := s!"__evt_topic{idx + 1}"
              let srcBase := indexedDynamicBaseOffsetExpr dynamicSource name
              let (encodeStmts, encodeLen) ←
                compileIndexedInPlaceEncoding dynamicSource p.ty srcBase (YulExpr.ident "__evt_ptr") s!"__evt_arg{idx}_indexed"
              pure (encodeStmts ++ [YulStmt.let_ topicName (YulExpr.call "keccak256" [
                YulExpr.ident "__evt_ptr",
                encodeLen
              ])], YulExpr.ident topicName)
          | _ =>
              throw s!"Compilation error: indexed dynamic composite event param '{p.name}' in event '{eventName}' currently requires direct parameter reference ({issue586Ref})."
        else
          let topicName := s!"__evt_topic{idx + 1}"
          let stores ← compileStaticCompositeEventWordStores dynamicSource eventName p.name p.ty
            srcExpr argExpr (YulExpr.ident "__evt_ptr")
          pure (stores ++ [YulStmt.let_ topicName (YulExpr.call "keccak256" [
            YulExpr.ident "__evt_ptr",
            YulExpr.lit (eventHeadWordSize p.ty)
          ])], YulExpr.ident topicName)
    | ParamType.adt _ maxFields =>
        let topicName := s!"__evt_topic{idx + 1}"
        let stores ← compileAdtEventWordStores eventName p.name srcExpr argExpr
          (YulExpr.ident "__evt_ptr") maxFields
        pure (stores ++ [YulStmt.let_ topicName (YulExpr.call "keccak256" [
          YulExpr.ident "__evt_ptr",
          YulExpr.lit (eventHeadWordSize p.ty)
        ])], YulExpr.ident topicName)
    | _ =>
        pure ([], normalizeEventWord p.ty argExpr)
  let indexedTopicStmts := indexedTopicParts.flatMap (·.1)
  let logFn := eventLogFunction indexed.length
  let logArgs := eventLogArgs dataSizeExpr indexedTopicParts
  let logStmt := YulStmt.exprStmt (YulExpr.call logFn logArgs)
  pure [YulStmt.block ([storePtr] ++ sigStores ++ [topic0Store] ++ indexedTopicStmts ++ unindexedTailInit ++ unindexedStores ++ [logStmt])]

end Compiler.CompilationModel
