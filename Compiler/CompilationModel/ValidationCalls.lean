/- 
  Compiler.CompilationModel.ValidationCalls: Call graph and identifier validation
-/
import Compiler.CompilationModel.Types
import Compiler.CompilationModel.AbiHelpers
import Compiler.CompilationModel.AbiTypeLayout
import Compiler.CompilationModel.DynamicData
import Compiler.CompilationModel.InternalNaming
import Compiler.CompilationModel.IssueRefs
import Compiler.CompilationModel.ScopeValidation
import Compiler.CompilationModel.UsageAnalysis

namespace Compiler.CompilationModel

def reservedExternalNames
    (mappingHelpersRequired arrayHelpersRequired arrayElementWordHelpersRequired
      paramDynamicHeadWordHelpersRequired
      mulDiv512HelpersRequired
      storageArrayHelpersRequired dynamicBytesEqHelpersRequired : Bool) : List String :=
  let mappingHelpers := if mappingHelpersRequired then ["mappingSlot"] else []
  let arrayHelpers :=
    if arrayHelpersRequired then
      [ checkedArrayElementCalldataHelperName
      , checkedArrayElementMemoryHelperName
      ]
    else
      []
  let arrayElementWordHelpers :=
    if arrayElementWordHelpersRequired then
      [ checkedArrayElementWordCalldataHelperName
      , checkedArrayElementWordMemoryHelperName
      , checkedArrayElementDynamicWordCalldataHelperName
      , checkedArrayElementDynamicWordMemoryHelperName
      , checkedArrayElementDynamicDataOffsetCalldataHelperName
      , checkedArrayElementDynamicDataOffsetMemoryHelperName
      ]
    else
      []
  let paramDynamicHeadWordHelpers :=
    if paramDynamicHeadWordHelpersRequired then
      [ checkedParamDynamicHeadWordCalldataHelperName
      , checkedParamDynamicHeadWordMemoryHelperName
      , checkedParamDynamicMemberLengthCalldataHelperName
      , checkedParamDynamicMemberLengthMemoryHelperName
      , checkedParamDynamicMemberDataOffsetCalldataHelperName
      , checkedParamDynamicMemberDataOffsetMemoryHelperName
      , checkedParamDynamicMemberElementCalldataHelperName
      , checkedParamDynamicMemberElementMemoryHelperName
      ]
    else
      []
  let mulDiv512Helpers :=
    if mulDiv512HelpersRequired then
      [ fullMulDivHelperName
      , fullMulDivUpHelperName
      ]
    else
      []
  let storageArrayHelpers :=
    if storageArrayHelpersRequired then
      [checkedStorageArrayElementHelperName]
    else
      []
  let dynamicBytesEqHelpers :=
    if dynamicBytesEqHelpersRequired then
      [dynamicBytesEqCalldataHelperName, dynamicBytesEqMemoryHelperName]
    else
      []
  let builtins := [builtinExpName]
  let entrypoints := ["fallback", "receive"]
  (mappingHelpers ++ arrayHelpers ++ arrayElementWordHelpers ++ paramDynamicHeadWordHelpers ++ mulDiv512Helpers ++ storageArrayHelpers ++ dynamicBytesEqHelpers ++ builtins ++ entrypoints).eraseDups

def firstReservedExternalCollision
    (spec : CompilationModel)
    (mappingHelpersRequired arrayHelpersRequired arrayElementWordHelpersRequired
      paramDynamicHeadWordHelpersRequired
      mulDiv512HelpersRequired
      storageArrayHelpersRequired dynamicBytesEqHelpersRequired : Bool) : Option String :=
  (spec.externals.map (·.name)).find? (fun name =>
    name.startsWith internalFunctionPrefix ||
      (reservedExternalNames
        mappingHelpersRequired
        arrayHelpersRequired
        arrayElementWordHelpersRequired
        paramDynamicHeadWordHelpersRequired
        mulDiv512HelpersRequired
        storageArrayHelpersRequired
        dynamicBytesEqHelpersRequired).contains name)

def internalDynamicParamSupported : ParamType → Bool
  | ParamType.array _ => true
  | ParamType.bytes | ParamType.string => true
  | ty@(ParamType.tuple _) => isDynamicParamType ty
  | ty@(ParamType.fixedArray _ _) => isDynamicParamType ty
  | _ => false

def firstUnsupportedInternalDynamicParam
    (fns : List FunctionSpec) : Option (String × String × ParamType) :=
  let rec goFns : List FunctionSpec → Option (String × String × ParamType)
    | [] => none
    | fn :: rest =>
        if !fn.isInternal then
          goFns rest
        else
          match fn.params.find? (fun p => isDynamicParamType p.ty && !internalDynamicParamSupported p.ty) with
          | some p => some (fn.name, p.name, p.ty)
          | none => goFns rest
  goFns fns

def internalCallYulArgCountForParam : ParamType → Nat
  | ParamType.array _ => 2
  | ParamType.bytes | ParamType.string => 2
  | ty@(ParamType.fixedArray _ _) =>
      if isDynamicParamType ty then 1 else (staticParamBindingNames "__arg" ty).length
  | ty@(ParamType.tuple _) =>
      if isDynamicParamType ty then 1 else (staticParamBindingNames "__arg" ty).length
  | ParamType.newtypeOf _ baseTy =>
      internalCallYulArgCountForParam baseTy
  | _ => 1

def internalCallYulArgCount (params : List Param) : Nat :=
  params.foldl (fun acc param => acc + internalCallYulArgCountForParam param.ty) 0

def internalReturnYulCountForType : ParamType → Nat
  | ParamType.array _ => 2
  | _ => 1

def internalReturnYulCount (returns : List ParamType) : Nat :=
  returns.foldl (fun acc retTy => acc + internalReturnYulCountForType retTy) 0

def linkedExternalCallYulArgCountForParam : ParamType → Nat
  | ParamType.array elemTy => if isSingleWordStaticParamType elemTy then 2 else 1
  | ParamType.bytes | ParamType.string => 2
  | _ => 1

def linkedExternalCallYulArgCount (params : List ParamType) : Nat :=
  params.foldl (fun acc paramTy => acc + linkedExternalCallYulArgCountForParam paramTy) 0

def linkedExternalReturnYulCountForType : ParamType → Nat
  | ParamType.tuple elemTys =>
      elemTys.foldl (fun acc elemTy => acc + linkedExternalReturnYulCountForType elemTy) 0
  | ParamType.fixedArray elemTy size =>
      size * linkedExternalReturnYulCountForType elemTy
  | _ => 1

def linkedExternalReturnYulCount (returns : List ParamType) : Nat :=
  returns.foldl (fun acc retTy => acc + linkedExternalReturnYulCountForType retTy) 0

def findInternalFunctionByName (functions : List FunctionSpec)
    (callerName calleeName : String) : Except String FunctionSpec := do
  let candidates := functions.filter (fun fn => fn.isInternal && fn.name == calleeName)
  match candidates with
  | [fn] => pure fn
  | [] =>
      throw s!"Compilation error: function '{callerName}' references unknown internal function '{calleeName}' ({issue625Ref})."
  | _ =>
      throw s!"Compilation error: function '{callerName}' references ambiguous internal function '{calleeName}' ({issue625Ref})."

mutual
def validateInternalCallShapesInExpr
    (functions : List FunctionSpec) (callerName : String) : Expr → Except String Unit
  | Expr.internalCall calleeName args => do
      validateInternalCallShapesInExprList functions callerName args
      let callee ← findInternalFunctionByName functions callerName calleeName
      let expectedArgs := internalCallYulArgCount callee.params
      if args.length != expectedArgs then
        throw s!"Compilation error: function '{callerName}' calls internal function '{calleeName}' with {args.length} Yul arg(s), expected {expectedArgs} ({issue625Ref})."
      let returns ← functionReturns callee
      if returns.length != 1 || internalReturnYulCount returns != 1 then
        throw s!"Compilation error: function '{callerName}' uses Expr.internalCall '{calleeName}' but callee returns {returns.length} logical value(s) / {internalReturnYulCount returns} Yul value(s); use Stmt.internalCallAssign for multi-return calls ({issue625Ref})."
  | Expr.call gas target value inOffset inSize outOffset outSize => do
      validateInternalCallShapesInExpr functions callerName gas
      validateInternalCallShapesInExpr functions callerName target
      validateInternalCallShapesInExpr functions callerName value
      validateInternalCallShapesInExpr functions callerName inOffset
      validateInternalCallShapesInExpr functions callerName inSize
      validateInternalCallShapesInExpr functions callerName outOffset
      validateInternalCallShapesInExpr functions callerName outSize
  | Expr.staticcall gas target inOffset inSize outOffset outSize => do
      validateInternalCallShapesInExpr functions callerName gas
      validateInternalCallShapesInExpr functions callerName target
      validateInternalCallShapesInExpr functions callerName inOffset
      validateInternalCallShapesInExpr functions callerName inSize
      validateInternalCallShapesInExpr functions callerName outOffset
      validateInternalCallShapesInExpr functions callerName outSize
  | Expr.delegatecall gas target inOffset inSize outOffset outSize => do
      validateInternalCallShapesInExpr functions callerName gas
      validateInternalCallShapesInExpr functions callerName target
      validateInternalCallShapesInExpr functions callerName inOffset
      validateInternalCallShapesInExpr functions callerName inSize
      validateInternalCallShapesInExpr functions callerName outOffset
      validateInternalCallShapesInExpr functions callerName outSize
  | Expr.extcodesize addr =>
      validateInternalCallShapesInExpr functions callerName addr
  | Expr.mload offset | Expr.tload offset =>
      validateInternalCallShapesInExpr functions callerName offset
  | Expr.calldataload offset =>
      validateInternalCallShapesInExpr functions callerName offset
  | Expr.keccak256 offset size => do
      validateInternalCallShapesInExpr functions callerName offset
      validateInternalCallShapesInExpr functions callerName size
  | Expr.returndataOptionalBoolAt outOffset =>
      validateInternalCallShapesInExpr functions callerName outOffset
  | Expr.mapping _ key =>
      validateInternalCallShapesInExpr functions callerName key
  | Expr.mappingWord _ key _ =>
      validateInternalCallShapesInExpr functions callerName key
  | Expr.mappingPackedWord _ key _ _ =>
      validateInternalCallShapesInExpr functions callerName key
  | Expr.mappingChain _ keys =>
      validateInternalCallShapesInExprList functions callerName keys
  | Expr.structMember _ key _ =>
      validateInternalCallShapesInExpr functions callerName key
  | Expr.mapping2 _ key1 key2 | Expr.mapping2Word _ key1 key2 _
  | Expr.structMember2 _ key1 key2 _ => do
      validateInternalCallShapesInExpr functions callerName key1
      validateInternalCallShapesInExpr functions callerName key2
  | Expr.mappingUint _ key =>
      validateInternalCallShapesInExpr functions callerName key
  | Expr.storageArrayElement _ index
  | Expr.arrayElement _ index
  | Expr.memoryArrayElement _ index
  | Expr.arrayElementWord _ index _ _
  | Expr.arrayElementDynamicWord _ index _
  | Expr.arrayElementDynamicDataOffset _ index
  | Expr.arrayElementDynamicMemberDataOffset _ index _
  | Expr.arrayElementDynamicMemberLength _ index _ =>
      validateInternalCallShapesInExpr functions callerName index
  | Expr.arrayElementDynamicMemberElement _ index _ innerIndex => do
      validateInternalCallShapesInExpr functions callerName index
      validateInternalCallShapesInExpr functions callerName innerIndex
  | Expr.paramDynamicMemberElement _ _ innerIndex =>
      validateInternalCallShapesInExpr functions callerName innerIndex
  | Expr.add a b | Expr.sub a b | Expr.mul a b | Expr.div a b | Expr.sdiv a b | Expr.mod a b | Expr.smod a b |
    Expr.bitAnd a b | Expr.bitOr a b | Expr.bitXor a b | Expr.shl a b | Expr.shr a b |
    Expr.sar a b | Expr.signextend a b | Expr.byte a b |
    Expr.eq a b | Expr.ge a b | Expr.gt a b | Expr.sgt a b | Expr.lt a b | Expr.slt a b | Expr.le a b |
    Expr.logicalAnd a b | Expr.logicalOr a b |
    Expr.wMulDown a b | Expr.wDivUp a b | Expr.min a b | Expr.max a b |
    Expr.ceilDiv a b => do
      validateInternalCallShapesInExpr functions callerName a
      validateInternalCallShapesInExpr functions callerName b
  | Expr.intrinsic _ _ _ args =>
      validateInternalCallShapesInExprList functions callerName args
  | Expr.forkIfAtLeast _ thenExpr elseExpr => do
      validateInternalCallShapesInExpr functions callerName thenExpr
      validateInternalCallShapesInExpr functions callerName elseExpr
  | Expr.mulDivDown a b c | Expr.mulDivUp a b c
  | Expr.mulDiv512Down a b c | Expr.mulDiv512Up a b c => do
      validateInternalCallShapesInExpr functions callerName a
      validateInternalCallShapesInExpr functions callerName b
      validateInternalCallShapesInExpr functions callerName c
  | Expr.bitNot a | Expr.logicalNot a =>
      validateInternalCallShapesInExpr functions callerName a
  | Expr.ite cond thenVal elseVal => do
      validateInternalCallShapesInExpr functions callerName cond
      validateInternalCallShapesInExpr functions callerName thenVal
      validateInternalCallShapesInExpr functions callerName elseVal
  | Expr.externalCall _ args =>
      validateInternalCallShapesInExprList functions callerName args
  | Expr.adtConstruct _ _ args =>
      validateInternalCallShapesInExprList functions callerName args
  -- Pure leaves: nothing to validate. Listed explicitly (rather than via
  -- `| _ => pure ()`) so the equation-lemma deriver does not have to
  -- enumerate the complement of every pattern above. Avoids the
  -- `_mutual.eq_def` 200 000-heartbeat ceiling when new `Expr` constructors
  -- land (e.g. verity#1832's `paramDynamicHeadWord`).
  | Expr.literal _ | Expr.param _ | Expr.constructorArg _
  | Expr.storage _ | Expr.storageAddr _
  | Expr.caller | Expr.contractAddress | Expr.txOrigin | Expr.chainid
  | Expr.msgValue | Expr.selfBalance
  | Expr.blockTimestamp | Expr.blockNumber | Expr.blobbasefee
  | Expr.calldatasize | Expr.returndataSize
  | Expr.localVar _
  | Expr.arrayLength _ | Expr.memoryArrayLength _ | Expr.storageArrayLength _
  | Expr.paramDynamicHeadWord _ _
  | Expr.paramDynamicStaticComposite _ _
  | Expr.paramDynamicMemberLength _ _
  | Expr.paramDynamicMemberDataOffset _ _
  | Expr.dynamicBytesEq _ _
  | Expr.adtTag _ _ | Expr.adtField _ _ _ _ _ =>
      pure ()
termination_by e => sizeOf e
decreasing_by all_goals simp_wf; all_goals omega

def validateInternalCallShapesInExprList
    (functions : List FunctionSpec) (callerName : String) : List Expr → Except String Unit
  | [] => pure ()
  | e :: es => do
      validateInternalCallShapesInExpr functions callerName e
      validateInternalCallShapesInExprList functions callerName es
termination_by es => sizeOf es
decreasing_by all_goals simp_wf; all_goals omega

def validateInternalCallShapesInStmt
    (functions : List FunctionSpec) (callerName : String) : Stmt → Except String Unit
  | Stmt.letVar _ value | Stmt.assignVar _ value | Stmt.setStorage _ value | Stmt.setStorageAddr _ value
  | Stmt.setStorageWord _ _ value |
    Stmt.storageArrayPush _ value |
    Stmt.return value | Stmt.require value _ =>
      validateInternalCallShapesInExpr functions callerName value
  | Stmt.setStorageArrayElement _ index value => do
      validateInternalCallShapesInExpr functions callerName index
      validateInternalCallShapesInExpr functions callerName value
  | Stmt.storageArrayPop _ =>
      pure ()
  | Stmt.requireError cond _ args => do
      validateInternalCallShapesInExpr functions callerName cond
      validateInternalCallShapesInExprList functions callerName args
  | Stmt.revertError _ args =>
      validateInternalCallShapesInExprList functions callerName args
  | Stmt.mstore offset value => do
      validateInternalCallShapesInExpr functions callerName offset
      validateInternalCallShapesInExpr functions callerName value
  | Stmt.tstore offset value => do
      validateInternalCallShapesInExpr functions callerName offset
      validateInternalCallShapesInExpr functions callerName value
  | Stmt.calldatacopy destOffset sourceOffset size => do
      validateInternalCallShapesInExpr functions callerName destOffset
      validateInternalCallShapesInExpr functions callerName sourceOffset
      validateInternalCallShapesInExpr functions callerName size
  | Stmt.returndataCopy destOffset sourceOffset size => do
      validateInternalCallShapesInExpr functions callerName destOffset
      validateInternalCallShapesInExpr functions callerName sourceOffset
      validateInternalCallShapesInExpr functions callerName size
  | Stmt.revertReturndata =>
      pure ()
  | Stmt.setMapping _ key value | Stmt.setMappingWord _ key _ value | Stmt.setMappingPackedWord _ key _ _ value | Stmt.setMappingUint _ key value
  | Stmt.setStructMember _ key _ value => do
      validateInternalCallShapesInExpr functions callerName key
      validateInternalCallShapesInExpr functions callerName value
  | Stmt.setMappingChain _ keys value => do
      validateInternalCallShapesInExprList functions callerName keys
      validateInternalCallShapesInExpr functions callerName value
  | Stmt.setMapping2 _ key1 key2 value | Stmt.setMapping2Word _ key1 key2 _ value
  | Stmt.setStructMember2 _ key1 key2 _ value => do
      validateInternalCallShapesInExpr functions callerName key1
      validateInternalCallShapesInExpr functions callerName key2
      validateInternalCallShapesInExpr functions callerName value
  | Stmt.ite cond thenBranch elseBranch => do
      validateInternalCallShapesInExpr functions callerName cond
      validateInternalCallShapesInStmtList functions callerName thenBranch
      validateInternalCallShapesInStmtList functions callerName elseBranch
  | Stmt.forEach _ count body => do
      validateInternalCallShapesInExpr functions callerName count
      validateInternalCallShapesInStmtList functions callerName body
  | Stmt.unsafeBlock _ body =>
      validateInternalCallShapesInStmtList functions callerName body
  | Stmt.matchAdt _ scrutinee branches => do
      validateInternalCallShapesInExpr functions callerName scrutinee
      validateInternalCallShapesInMatchBranches functions callerName branches
  | Stmt.emit _ args =>
      validateInternalCallShapesInExprList functions callerName args
  | Stmt.returnValues values =>
      validateInternalCallShapesInExprList functions callerName values
  | Stmt.internalCall calleeName args => do
      validateInternalCallShapesInExprList functions callerName args
      let callee ← findInternalFunctionByName functions callerName calleeName
      let expectedArgs := internalCallYulArgCount callee.params
      if args.length != expectedArgs then
        throw s!"Compilation error: function '{callerName}' calls internal function '{calleeName}' with {args.length} Yul arg(s), expected {expectedArgs} ({issue625Ref})."
      let returns ← functionReturns callee
      if !returns.isEmpty then
        throw s!"Compilation error: function '{callerName}' uses Stmt.internalCall '{calleeName}' but callee returns {returns.length} values; use Expr.internalCall for single-return or Stmt.internalCallAssign for multi-return calls ({issue625Ref})."
  | Stmt.internalCallAssign names calleeName args => do
      if names.isEmpty then
        throw s!"Compilation error: function '{callerName}' uses Stmt.internalCallAssign with no target variables ({issue625Ref})."
      let rec firstDuplicateTarget (seen : List String) : List String → Option String
        | [] => none
        | name :: rest =>
            if seen.contains name then
              some name
            else
              firstDuplicateTarget (name :: seen) rest
      match firstDuplicateTarget [] names with
      | some dup =>
          throw s!"Compilation error: function '{callerName}' uses Stmt.internalCallAssign with duplicate target '{dup}' ({issue625Ref})."
      | none =>
          pure ()
      validateInternalCallShapesInExprList functions callerName args
      let callee ← findInternalFunctionByName functions callerName calleeName
      let expectedArgs := internalCallYulArgCount callee.params
      if args.length != expectedArgs then
        throw s!"Compilation error: function '{callerName}' calls internal function '{calleeName}' with {args.length} Yul arg(s), expected {expectedArgs} ({issue625Ref})."
      let returns ← functionReturns callee
      let expectedReturns := internalReturnYulCount returns
      if expectedReturns != names.length then
        throw s!"Compilation error: function '{callerName}' binds {names.length} Yul value(s) from internal function '{calleeName}', but callee returns {returns.length} logical value(s) / {expectedReturns} Yul value(s) ({issue625Ref})."
  | Stmt.rawLog topics dataOffset dataSize => do
      validateInternalCallShapesInExprList functions callerName topics
      validateInternalCallShapesInExpr functions callerName dataOffset
      validateInternalCallShapesInExpr functions callerName dataSize
  | Stmt.externalCallBind _resultVars _ args =>
      validateInternalCallShapesInExprList functions callerName args
  | Stmt.tryExternalCallBind _ _resultVars _ args =>
      validateInternalCallShapesInExprList functions callerName args
  | Stmt.ecm _ args =>
      validateInternalCallShapesInExprList functions callerName args
  | _ =>
      pure ()
termination_by s => sizeOf s
decreasing_by all_goals simp_wf; all_goals omega

def validateInternalCallShapesInStmtList
    (functions : List FunctionSpec) (callerName : String) : List Stmt → Except String Unit
  | [] => pure ()
  | s :: ss => do
      validateInternalCallShapesInStmt functions callerName s
      validateInternalCallShapesInStmtList functions callerName ss
termination_by ss => sizeOf ss
decreasing_by all_goals simp_wf; all_goals omega

def validateInternalCallShapesInMatchBranches
    (functions : List FunctionSpec) (callerName : String) :
    List (String × List String × List Stmt) → Except String Unit
  | [] => pure ()
  | (_, _, body) :: rest => do
      validateInternalCallShapesInStmtList functions callerName body
      validateInternalCallShapesInMatchBranches functions callerName rest
termination_by bs => sizeOf bs
decreasing_by all_goals simp_wf; all_goals omega
end

def validateInternalCallShapesInFunction (functions : List FunctionSpec)
    (spec : FunctionSpec) : Except String Unit := do
  spec.body.forM (validateInternalCallShapesInStmt functions spec.name)

mutual
def validateExternalCallTargetsInExpr
    (externals : List ExternalFunction) (context : String) : Expr → Except String Unit
  | Expr.externalCall name args => do
      if name == builtinExpName then
        if args.length != 2 then
          throw s!"Compilation error: {context} calls builtin exp with {args.length} args, expected 2."
      else
        match externals.find? (fun ext => ext.name == name) with
        | none =>
            throw s!"Compilation error: {context} references unknown external call target '{name}' ({issue732Ref}). Declare it in spec.externals."
        | some ext =>
            let expectedArgs := linkedExternalCallYulArgCount ext.params
            if args.length != expectedArgs then
              throw s!"Compilation error: {context} calls external '{name}' with {args.length} Yul arg(s), expected {expectedArgs} ({issue184Ref})."
            let returns ← externalFunctionReturns ext
            if returns.length != 1 then
              throw s!"Compilation error: {context} uses Expr.externalCall '{name}' but spec.externals declares {returns.length} return values; Expr.externalCall requires exactly 1 ({issue184Ref})."
      validateExternalCallTargetsInExprList externals context args
  | Expr.call gas target value inOffset inSize outOffset outSize => do
      validateExternalCallTargetsInExpr externals context gas
      validateExternalCallTargetsInExpr externals context target
      validateExternalCallTargetsInExpr externals context value
      validateExternalCallTargetsInExpr externals context inOffset
      validateExternalCallTargetsInExpr externals context inSize
      validateExternalCallTargetsInExpr externals context outOffset
      validateExternalCallTargetsInExpr externals context outSize
  | Expr.staticcall gas target inOffset inSize outOffset outSize => do
      validateExternalCallTargetsInExpr externals context gas
      validateExternalCallTargetsInExpr externals context target
      validateExternalCallTargetsInExpr externals context inOffset
      validateExternalCallTargetsInExpr externals context inSize
      validateExternalCallTargetsInExpr externals context outOffset
      validateExternalCallTargetsInExpr externals context outSize
  | Expr.delegatecall gas target inOffset inSize outOffset outSize => do
      validateExternalCallTargetsInExpr externals context gas
      validateExternalCallTargetsInExpr externals context target
      validateExternalCallTargetsInExpr externals context inOffset
      validateExternalCallTargetsInExpr externals context inSize
      validateExternalCallTargetsInExpr externals context outOffset
      validateExternalCallTargetsInExpr externals context outSize
  | Expr.extcodesize addr =>
      validateExternalCallTargetsInExpr externals context addr
  | Expr.mload offset | Expr.tload offset =>
      validateExternalCallTargetsInExpr externals context offset
  | Expr.calldataload offset =>
      validateExternalCallTargetsInExpr externals context offset
  | Expr.keccak256 offset size => do
      validateExternalCallTargetsInExpr externals context offset
      validateExternalCallTargetsInExpr externals context size
  | Expr.returndataOptionalBoolAt outOffset =>
      validateExternalCallTargetsInExpr externals context outOffset
  | Expr.mapping _ key =>
      validateExternalCallTargetsInExpr externals context key
  | Expr.mappingWord _ key _ =>
      validateExternalCallTargetsInExpr externals context key
  | Expr.mappingPackedWord _ key _ _ =>
      validateExternalCallTargetsInExpr externals context key
  | Expr.mappingChain _ keys =>
      validateExternalCallTargetsInExprList externals context keys
  | Expr.structMember _ key _ =>
      validateExternalCallTargetsInExpr externals context key
  | Expr.mapping2 _ key1 key2 | Expr.mapping2Word _ key1 key2 _
  | Expr.structMember2 _ key1 key2 _ => do
      validateExternalCallTargetsInExpr externals context key1
      validateExternalCallTargetsInExpr externals context key2
  | Expr.mappingUint _ key =>
      validateExternalCallTargetsInExpr externals context key
  | Expr.internalCall _ args =>
      validateExternalCallTargetsInExprList externals context args
  | Expr.storageArrayElement _ index
  | Expr.arrayElement _ index
  | Expr.memoryArrayElement _ index
  | Expr.arrayElementWord _ index _ _
  | Expr.arrayElementDynamicWord _ index _
  | Expr.arrayElementDynamicDataOffset _ index
  | Expr.arrayElementDynamicMemberDataOffset _ index _
  | Expr.arrayElementDynamicMemberLength _ index _ =>
      validateExternalCallTargetsInExpr externals context index
  | Expr.arrayElementDynamicMemberElement _ index _ innerIndex => do
      validateExternalCallTargetsInExpr externals context index
      validateExternalCallTargetsInExpr externals context innerIndex
  | Expr.paramDynamicMemberElement _ _ innerIndex =>
      validateExternalCallTargetsInExpr externals context innerIndex
  | Expr.add a b | Expr.sub a b | Expr.mul a b | Expr.div a b | Expr.sdiv a b | Expr.mod a b | Expr.smod a b |
    Expr.bitAnd a b | Expr.bitOr a b | Expr.bitXor a b | Expr.shl a b | Expr.shr a b |
    Expr.sar a b | Expr.signextend a b | Expr.byte a b |
    Expr.eq a b | Expr.ge a b | Expr.gt a b | Expr.sgt a b | Expr.lt a b | Expr.slt a b | Expr.le a b |
    Expr.logicalAnd a b | Expr.logicalOr a b |
    Expr.wMulDown a b | Expr.wDivUp a b | Expr.min a b | Expr.max a b |
    Expr.ceilDiv a b => do
      validateExternalCallTargetsInExpr externals context a
      validateExternalCallTargetsInExpr externals context b
  | Expr.intrinsic _ _ _ args =>
      validateExternalCallTargetsInExprList externals context args
  | Expr.forkIfAtLeast _ thenExpr elseExpr => do
      validateExternalCallTargetsInExpr externals context thenExpr
      validateExternalCallTargetsInExpr externals context elseExpr
  | Expr.mulDivDown a b c | Expr.mulDivUp a b c
  | Expr.mulDiv512Down a b c | Expr.mulDiv512Up a b c => do
      validateExternalCallTargetsInExpr externals context a
      validateExternalCallTargetsInExpr externals context b
      validateExternalCallTargetsInExpr externals context c
  | Expr.bitNot a | Expr.logicalNot a =>
      validateExternalCallTargetsInExpr externals context a
  | Expr.ite cond thenVal elseVal => do
      validateExternalCallTargetsInExpr externals context cond
      validateExternalCallTargetsInExpr externals context thenVal
      validateExternalCallTargetsInExpr externals context elseVal
  | Expr.adtConstruct _ _ args =>
      validateExternalCallTargetsInExprList externals context args
  -- Pure leaves: nothing to validate. Listed explicitly (rather than via
  -- `| _ => pure ()`) so the equation-lemma deriver does not have to
  -- enumerate the complement of every pattern above. Avoids the
  -- `_mutual.eq_def` 200 000-heartbeat ceiling when new `Expr` constructors
  -- land (e.g. verity#1832's `paramDynamicHeadWord`).
  | Expr.literal _ | Expr.param _ | Expr.constructorArg _
  | Expr.storage _ | Expr.storageAddr _
  | Expr.caller | Expr.contractAddress | Expr.txOrigin | Expr.chainid
  | Expr.msgValue | Expr.selfBalance
  | Expr.blockTimestamp | Expr.blockNumber | Expr.blobbasefee
  | Expr.calldatasize | Expr.returndataSize
  | Expr.localVar _
  | Expr.arrayLength _ | Expr.memoryArrayLength _ | Expr.storageArrayLength _
  | Expr.paramDynamicHeadWord _ _
  | Expr.paramDynamicStaticComposite _ _
  | Expr.paramDynamicMemberLength _ _
  | Expr.paramDynamicMemberDataOffset _ _
  | Expr.dynamicBytesEq _ _
  | Expr.adtTag _ _ | Expr.adtField _ _ _ _ _ =>
      pure ()
termination_by e => sizeOf e
decreasing_by all_goals simp_wf; all_goals omega

def validateExternalCallTargetsInExprList
    (externals : List ExternalFunction) (context : String) : List Expr → Except String Unit
  | [] => pure ()
  | e :: es => do
      validateExternalCallTargetsInExpr externals context e
      validateExternalCallTargetsInExprList externals context es
termination_by es => sizeOf es
decreasing_by all_goals simp_wf; all_goals omega

def validateExternalCallTargetsInStmt
    (externals : List ExternalFunction) (context : String) : Stmt → Except String Unit
  | Stmt.letVar _ value | Stmt.assignVar _ value | Stmt.setStorage _ value | Stmt.setStorageAddr _ value
  | Stmt.setStorageWord _ _ value |
    Stmt.storageArrayPush _ value |
    Stmt.return value | Stmt.require value _ =>
      validateExternalCallTargetsInExpr externals context value
  | Stmt.setStorageArrayElement _ index value => do
      validateExternalCallTargetsInExpr externals context index
      validateExternalCallTargetsInExpr externals context value
  | Stmt.storageArrayPop _ =>
      pure ()
  | Stmt.requireError cond _ args => do
      validateExternalCallTargetsInExpr externals context cond
      validateExternalCallTargetsInExprList externals context args
  | Stmt.revertError _ args =>
      validateExternalCallTargetsInExprList externals context args
  | Stmt.mstore offset value => do
      validateExternalCallTargetsInExpr externals context offset
      validateExternalCallTargetsInExpr externals context value
  | Stmt.tstore offset value => do
      validateExternalCallTargetsInExpr externals context offset
      validateExternalCallTargetsInExpr externals context value
  | Stmt.calldatacopy destOffset sourceOffset size => do
      validateExternalCallTargetsInExpr externals context destOffset
      validateExternalCallTargetsInExpr externals context sourceOffset
      validateExternalCallTargetsInExpr externals context size
  | Stmt.returndataCopy destOffset sourceOffset size => do
      validateExternalCallTargetsInExpr externals context destOffset
      validateExternalCallTargetsInExpr externals context sourceOffset
      validateExternalCallTargetsInExpr externals context size
  | Stmt.revertReturndata =>
      pure ()
  | Stmt.setMapping _ key value | Stmt.setMappingWord _ key _ value | Stmt.setMappingPackedWord _ key _ _ value | Stmt.setMappingUint _ key value
  | Stmt.setStructMember _ key _ value => do
      validateExternalCallTargetsInExpr externals context key
      validateExternalCallTargetsInExpr externals context value
  | Stmt.setMappingChain _ keys value => do
      validateExternalCallTargetsInExprList externals context keys
      validateExternalCallTargetsInExpr externals context value
  | Stmt.setMapping2 _ key1 key2 value | Stmt.setMapping2Word _ key1 key2 _ value
  | Stmt.setStructMember2 _ key1 key2 _ value => do
      validateExternalCallTargetsInExpr externals context key1
      validateExternalCallTargetsInExpr externals context key2
      validateExternalCallTargetsInExpr externals context value
  | Stmt.ite cond thenBranch elseBranch => do
      validateExternalCallTargetsInExpr externals context cond
      validateExternalCallTargetsInStmtList externals context thenBranch
      validateExternalCallTargetsInStmtList externals context elseBranch
  | Stmt.forEach _ count body => do
      validateExternalCallTargetsInExpr externals context count
      validateExternalCallTargetsInStmtList externals context body
  | Stmt.unsafeBlock _ body =>
      validateExternalCallTargetsInStmtList externals context body
  | Stmt.matchAdt _ scrutinee branches => do
      validateExternalCallTargetsInExpr externals context scrutinee
      validateExternalCallTargetsInMatchBranches externals context branches
  | Stmt.emit _ args =>
      validateExternalCallTargetsInExprList externals context args
  | Stmt.internalCall _ args =>
      validateExternalCallTargetsInExprList externals context args
  | Stmt.internalCallAssign _ _ args =>
      validateExternalCallTargetsInExprList externals context args
  | Stmt.externalCallBind resultVars externalName args => do
      validateExternalCallTargetsInExprList externals context args
      match externals.find? (fun ext => ext.name == externalName) with
      | none =>
          throw s!"Compilation error: {context} uses Stmt.externalCallBind with unknown external function '{externalName}'."
      | some ext => do
          let expectedArgs := linkedExternalCallYulArgCount ext.params
          if args.length != expectedArgs then
            throw s!"Compilation error: {context} calls external function '{externalName}' with {args.length} Yul arg(s), expected {expectedArgs}."
          let returns ← externalFunctionReturns ext
          let expectedReturns := linkedExternalReturnYulCount returns
          if expectedReturns != resultVars.length then
            throw s!"Compilation error: {context} binds {resultVars.length} Yul value(s) from external function '{externalName}', but it returns {expectedReturns} Yul value(s)."
          let rec checkDuplicateVars (seen : List String) : List String → Except String Unit
            | [] => pure ()
            | name :: rest =>
                if seen.contains name then
                  throw s!"Compilation error: {context} uses Stmt.externalCallBind with duplicate result variable '{name}'."
                else
                  checkDuplicateVars (name :: seen) rest
          checkDuplicateVars [] resultVars
  | Stmt.tryExternalCallBind successVar resultVars externalName args => do
      validateExternalCallTargetsInExprList externals context args
      match externals.find? (fun ext => ext.name == externalName) with
      | none =>
          throw s!"Compilation error: {context} uses Stmt.tryExternalCallBind with unknown external function '{externalName}'."
      | some ext => do
          let expectedArgs := linkedExternalCallYulArgCount ext.params
          if args.length != expectedArgs then
            throw s!"Compilation error: {context} calls external function '{externalName}' with {args.length} Yul arg(s), expected {expectedArgs}."
          let returns ← externalFunctionReturns ext
          let expectedReturns := linkedExternalReturnYulCount returns
          if expectedReturns != resultVars.length then
            throw s!"Compilation error: {context} binds {resultVars.length} Yul value(s) from external function '{externalName}', but it returns {expectedReturns} Yul value(s)."
          let tryName := s!"{externalName}_try"
          match externals.find? (fun candidate => candidate.name == tryName) with
          | none =>
              throw s!"Compilation error: {context} uses Stmt.tryExternalCallBind for external function '{externalName}', but required linked wrapper '{tryName}' is not declared."
          | some tryExt => do
              if tryExt.params != ext.params then
                throw s!"Compilation error: try wrapper '{tryName}' must take the same parameters as external function '{externalName}'."
              let tryReturns ← externalFunctionReturns tryExt
              let validTryReturns :=
                match tryReturns with
                | ParamType.bool :: tryTail =>
                    linkedExternalReturnYulCount tryTail == linkedExternalReturnYulCount returns
                | _ => false
              if !validTryReturns then
                throw s!"Compilation error: try wrapper '{tryName}' must return Bool followed by the flattened return values of external function '{externalName}'."
          let allVars := successVar :: resultVars
          let rec checkDuplicateTryVars (seen : List String) : List String → Except String Unit
            | [] => pure ()
            | name :: rest =>
                if seen.contains name then
                  throw s!"Compilation error: {context} uses Stmt.tryExternalCallBind with duplicate result variable '{name}'."
                else
                  checkDuplicateTryVars (name :: seen) rest
          checkDuplicateTryVars [] allVars
  | Stmt.returnValues values =>
      validateExternalCallTargetsInExprList externals context values
  | Stmt.rawLog topics dataOffset dataSize => do
      validateExternalCallTargetsInExprList externals context topics
      validateExternalCallTargetsInExpr externals context dataOffset
      validateExternalCallTargetsInExpr externals context dataSize
  | Stmt.ecm _ args =>
      validateExternalCallTargetsInExprList externals context args
  | _ =>
      pure ()
termination_by s => sizeOf s
decreasing_by all_goals simp_wf; all_goals omega

def validateExternalCallTargetsInStmtList
    (externals : List ExternalFunction) (context : String) : List Stmt → Except String Unit
  | [] => pure ()
  | s :: ss => do
      validateExternalCallTargetsInStmt externals context s
      validateExternalCallTargetsInStmtList externals context ss
termination_by ss => sizeOf ss
decreasing_by all_goals simp_wf; all_goals omega

def validateExternalCallTargetsInMatchBranches
    (externals : List ExternalFunction) (context : String) :
    List (String × List String × List Stmt) → Except String Unit
  | [] => pure ()
  | (_, _, body) :: rest => do
      validateExternalCallTargetsInStmtList externals context body
      validateExternalCallTargetsInMatchBranches externals context rest
termination_by bs => sizeOf bs
decreasing_by all_goals simp_wf; all_goals omega
end

def validateExternalCallTargetsInFunction
    (externals : List ExternalFunction) (spec : FunctionSpec) : Except String Unit := do
  spec.body.forM (validateExternalCallTargetsInStmt externals s!"function '{spec.name}'")

def validateExternalCallTargetsInConstructor
    (externals : List ExternalFunction) (ctor : Option ConstructorSpec) : Except String Unit := do
  match ctor with
  | none => pure ()
  | some spec =>
      spec.body.forM (validateExternalCallTargetsInStmt externals "constructor")

mutual
def supportedCustomErrorParamType : ParamType → Bool
  | ParamType.uint256 | ParamType.int256 | ParamType.uint8 | ParamType.uint16 | ParamType.address | ParamType.bool | ParamType.bytes32 | ParamType.bytes | ParamType.string => true
  | ParamType.array elemTy => supportedCustomErrorParamType elemTy
  | ParamType.fixedArray elemTy _ => supportedCustomErrorParamType elemTy
  | ParamType.tuple elemTys => supportedCustomErrorParamTypes elemTys
  | ParamType.adt _ _ => true
  | ParamType.newtypeOf _ baseType => supportedCustomErrorParamType baseType
termination_by ty => sizeOf ty
decreasing_by
  all_goals simp_wf
  all_goals omega

def supportedCustomErrorParamTypes : List ParamType → Bool
  | [] => true
  | ty :: tys => supportedCustomErrorParamType ty && supportedCustomErrorParamTypes tys
termination_by tys => sizeOf tys
decreasing_by
  all_goals simp_wf
  all_goals omega
end

def validateErrorDef (err : ErrorDef) : Except String Unit := do
  for ty in err.params do
    if !supportedCustomErrorParamType ty then
      throw s!"Compilation error: custom error '{err.name}' uses unsupported dynamic parameter type {repr ty} ({issue586Ref}). Use uint256/address/bool/bytes32/bytes/string parameters."

def validateEventDef (eventDef : EventDef) : Except String Unit := do
  let indexedCount := eventDef.params.foldl
    (fun acc p => if p.kind == EventParamKind.indexed then acc + 1 else acc)
    0
  if indexedCount > 3 then
    throw s!"Compilation error: event '{eventDef.name}' has {indexedCount} indexed params; max is 3"

def ensureContractIdentifier (kind name : String) : Except String Unit := do
  match Compiler.ensureValidIdentifier kind name with
  | .ok _ => pure ()
  | .error err => throw s!"Compilation error: {err}"

private def ensureAbiInterfaceExternalIdentifier (name : String) : Except String Unit := do
  match name.splitOn "." with
  | [iface, method] => do
      ensureContractIdentifier "external interface" iface
      ensureContractIdentifier "external interface method" method
  | _ =>
      throw s!"Compilation error: external ABI declaration name must be '<Interface>.<method>': {name}"

/-- Source identifiers that lower directly to Yul variables must avoid the
compiler-reserved `__` prefix used by dispatch and scratch temporaries. -/
private def ensureNonReservedYulIdentifier (kind name : String) : Except String Unit := do
  if name.startsWith "__" then
    throw s!"Compilation error: {kind} '{name}' uses reserved compiler prefix '__' ({issue756Ref}). Rename it."

private def isInternalImmutableStorageName (name : String) : Bool :=
  name.startsWith "__immutable_"

private def validateContractIdentifiers (kind : String) : List String → Except String Unit
  | [] => pure ()
  | name :: rest => do
      ensureContractIdentifier kind name
      ensureNonReservedYulIdentifier kind name
      validateContractIdentifiers kind rest

private def validateFieldIdentifiers : List Field → Except String Unit
  | [] => pure ()
  | field :: rest => do
      ensureContractIdentifier "field" field.name
      if !isInternalImmutableStorageName field.name then
        ensureNonReservedYulIdentifier "field" field.name
      validateFieldIdentifiers rest

private def validateFunctionYulIdentifiers (fn : FunctionSpec) : Except String Unit := do
  validateContractIdentifiers "function parameter" (fn.params.map (·.name))
  validateContractIdentifiers "local binder" (collectStmtListBindNames fn.body)
  validateContractIdentifiers "assignment target" (collectStmtListAssignedNames fn.body)

def validateFunctionIdentifiers (fn : FunctionSpec) : Except String Unit := do
  ensureContractIdentifier "function" fn.name
  validateFunctionYulIdentifiers fn

private def validateConstructorYulIdentifiers (ctor : ConstructorSpec) : Except String Unit := do
  validateContractIdentifiers "constructor parameter" (ctor.params.map (·.name))
  validateContractIdentifiers "local binder" (collectStmtListBindNames ctor.body)
  for name in collectStmtListAssignedNames ctor.body do
    ensureContractIdentifier "assignment target" name
    if !isInternalImmutableStorageName name then
      ensureNonReservedYulIdentifier "assignment target" name

def validateConstructorIdentifiers (ctor : ConstructorSpec) : Except String Unit :=
  validateConstructorYulIdentifiers ctor

private def validateFunctionIdentifierList : List FunctionSpec → Except String Unit
  | [] => pure ()
  | fn :: rest => do
      validateFunctionIdentifiers fn
      validateFunctionIdentifierList rest

private def validateReservedCompilerIdentifiers (spec : CompilationModel) : Except String Unit := do
  validateFieldIdentifiers spec.fields
  validateFunctionIdentifierList spec.functions
  match spec.constructor with
  | none => pure ()
  | some ctor =>
      validateConstructorIdentifiers ctor

def validateIdentifierShapes (spec : CompilationModel) : Except String Unit := do
  ensureContractIdentifier "contract" spec.name
  validateReservedCompilerIdentifiers spec
  for eventDef in spec.events do
    ensureContractIdentifier "event" eventDef.name
    for p in eventDef.params do
      ensureContractIdentifier "event parameter" p.name
  for err in spec.errors do
    ensureContractIdentifier "custom error" err.name
  for ext in spec.externals do
    if ext.linkMode == .external && ext.name.contains '.' then
      -- Dotted ABI-interface externals (e.g. `IPool.supply`) lower by selector, never
      -- as a Yul identifier. Other external forms are linked/called by their name.
      ensureAbiInterfaceExternalIdentifier ext.name
    else
      ensureContractIdentifier "external declaration" ext.name

private theorem ensureNonReservedYulIdentifier_ok
    {kind name : String}
    (hvalidate : ensureNonReservedYulIdentifier kind name = Except.ok ()) :
    ¬ name.startsWith "__" := by
  by_cases hreserved : name.startsWith "__"
  · simp [ensureNonReservedYulIdentifier, hreserved] at hvalidate
  · exact hreserved

private theorem validateContractIdentifiers_ok_of_mem
    {kind name : String}
    {names : List String}
    (hvalidate : validateContractIdentifiers kind names = Except.ok ())
    (hmem : name ∈ names) :
    ¬ name.startsWith "__" := by
  induction names with
  | nil =>
      cases hmem
  | cons head tail ih =>
      cases hcontract : ensureContractIdentifier kind head with
      | error err =>
          simp [validateContractIdentifiers, hcontract] at hvalidate
          cases hvalidate
      | ok _ =>
          cases hreserved : ensureNonReservedYulIdentifier kind head with
          | error err =>
              simp [validateContractIdentifiers, hcontract, hreserved] at hvalidate
              cases hvalidate
          | ok _ =>
              have htail : validateContractIdentifiers kind tail = Except.ok () := by
                simpa [validateContractIdentifiers, hcontract, hreserved] using hvalidate
              simp at hmem
              rcases hmem with rfl | hmem
              · exact ensureNonReservedYulIdentifier_ok hreserved
              · exact ih htail hmem

private theorem validateFieldIdentifiers_ok_of_mem
    {fields : List Field}
    {field : Field}
    (hvalidate : validateFieldIdentifiers fields = Except.ok ())
    (hmem : field ∈ fields) :
    ¬ (field.name.startsWith "__" && !isInternalImmutableStorageName field.name) := by
  induction fields with
  | nil =>
      cases hmem
  | cons head tail ih =>
      cases hcontract : ensureContractIdentifier "field" head.name with
      | error err =>
          simp [validateFieldIdentifiers, hcontract] at hvalidate
          cases hvalidate
      | ok _ =>
          by_cases himm : isInternalImmutableStorageName head.name
          · have htail : validateFieldIdentifiers tail = Except.ok () := by
              simpa [validateFieldIdentifiers, hcontract, himm] using hvalidate
            simp at hmem
            rcases hmem with rfl | hmem
            · simp [himm]
            · exact ih htail hmem
          · cases hreserved : ensureNonReservedYulIdentifier "field" head.name with
            | error err =>
                simp [validateFieldIdentifiers, hcontract, himm, hreserved] at hvalidate
                cases hvalidate
            | ok _ =>
                have htail : validateFieldIdentifiers tail = Except.ok () := by
                  simpa [validateFieldIdentifiers, hcontract, himm, hreserved] using hvalidate
                simp at hmem
                rcases hmem with rfl | hmem
                · intro hbad
                  have hbadReserved := hbad
                  simp [himm] at hbadReserved
                  exact (ensureNonReservedYulIdentifier_ok hreserved) hbadReserved
                · exact ih htail hmem

private theorem validateFunctionYulIdentifiers_params_ok_of_mem
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateFunctionYulIdentifiers fn = Except.ok ())
    (hmem : name ∈ fn.params.map (·.name)) :
    ¬ name.startsWith "__" := by
  have hparams :
      validateContractIdentifiers "function parameter" (fn.params.map (·.name)) = Except.ok () := by
    unfold validateFunctionYulIdentifiers at hvalidate
    cases hparams : validateContractIdentifiers "function parameter" (fn.params.map (·.name)) with
    | error err =>
        simp [hparams] at hvalidate
        cases hvalidate
    | ok a =>
        cases a
        rfl
  exact validateContractIdentifiers_ok_of_mem hparams hmem

private theorem validateFunctionYulIdentifiers_locals_ok_of_mem
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateFunctionYulIdentifiers fn = Except.ok ())
    (hmem : name ∈ collectStmtListBindNames fn.body) :
    ¬ name.startsWith "__" := by
  have hparams :
      validateContractIdentifiers "function parameter" (fn.params.map (·.name)) = Except.ok () := by
    unfold validateFunctionYulIdentifiers at hvalidate
    cases hparams : validateContractIdentifiers "function parameter" (fn.params.map (·.name)) with
    | error err =>
        simp [hparams] at hvalidate
        cases hvalidate
    | ok a =>
        cases a
        rfl
  have hlocals :
      validateContractIdentifiers "local binder" (collectStmtListBindNames fn.body) = Except.ok () := by
    unfold validateFunctionYulIdentifiers at hvalidate
    cases hlocals : validateContractIdentifiers "local binder" (collectStmtListBindNames fn.body) with
    | error err =>
        simp [hparams, hlocals] at hvalidate
        cases hvalidate
    | ok a =>
        cases a
        rfl
  exact validateContractIdentifiers_ok_of_mem hlocals hmem

private theorem validateFunctionYulIdentifiers_assignTargets_ok_of_mem
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateFunctionYulIdentifiers fn = Except.ok ())
    (hmem : name ∈ collectStmtListAssignedNames fn.body) :
    ¬ name.startsWith "__" := by
  have hparams :
      validateContractIdentifiers "function parameter" (fn.params.map (·.name)) = Except.ok () := by
    unfold validateFunctionYulIdentifiers at hvalidate
    cases hparams : validateContractIdentifiers "function parameter" (fn.params.map (·.name)) with
    | error err =>
        simp [hparams] at hvalidate
        cases hvalidate
    | ok a =>
        cases a
        rfl
  have hlocals :
      validateContractIdentifiers "local binder" (collectStmtListBindNames fn.body) = Except.ok () := by
    unfold validateFunctionYulIdentifiers at hvalidate
    cases hlocals : validateContractIdentifiers "local binder" (collectStmtListBindNames fn.body) with
    | error err =>
        simp [hparams, hlocals] at hvalidate
        cases hvalidate
    | ok a =>
        cases a
        rfl
  have hassign :
      validateContractIdentifiers "assignment target" (collectStmtListAssignedNames fn.body) = Except.ok () := by
    unfold validateFunctionYulIdentifiers at hvalidate
    simpa [hparams, hlocals] using hvalidate
  exact validateContractIdentifiers_ok_of_mem hassign hmem

private theorem validateFunctionIdentifierList_ok_of_mem
    {functions : List FunctionSpec}
    {fn : FunctionSpec}
    (hvalidate : validateFunctionIdentifierList functions = Except.ok ())
    (hmem : fn ∈ functions) :
    validateFunctionIdentifiers fn = Except.ok () := by
  induction functions with
  | nil =>
      cases hmem
  | cons head tail ih =>
      cases hhead : validateFunctionIdentifiers head with
      | error err =>
          simp [validateFunctionIdentifierList, hhead] at hvalidate
          cases hvalidate
      | ok _ =>
          have htail : validateFunctionIdentifierList tail = Except.ok () := by
            simpa [validateFunctionIdentifierList, hhead] using hvalidate
          simp at hmem
          rcases hmem with rfl | hmem
          · simp [hhead]
          · exact ih htail hmem

theorem validateFunctionIdentifiers_params_avoidReservedCompilerPrefix
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateFunctionIdentifiers fn = Except.ok ())
    (hmem : name ∈ fn.params.map (·.name)) :
    ¬ name.startsWith "__" := by
  have hyul :
      validateFunctionYulIdentifiers fn = Except.ok () := by
    unfold validateFunctionIdentifiers at hvalidate
    cases hname : ensureContractIdentifier "function" fn.name with
    | error err =>
        simp [hname] at hvalidate
        cases hvalidate
    | ok _ =>
        simpa [validateFunctionIdentifiers, hname] using hvalidate
  exact validateFunctionYulIdentifiers_params_ok_of_mem hyul hmem

theorem validateFunctionIdentifiers_locals_avoidReservedCompilerPrefix
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateFunctionIdentifiers fn = Except.ok ())
    (hmem : name ∈ collectStmtListBindNames fn.body) :
    ¬ name.startsWith "__" := by
  have hyul :
      validateFunctionYulIdentifiers fn = Except.ok () := by
    unfold validateFunctionIdentifiers at hvalidate
    cases hname : ensureContractIdentifier "function" fn.name with
    | error err =>
        simp [hname] at hvalidate
        cases hvalidate
    | ok _ =>
        simpa [validateFunctionIdentifiers, hname] using hvalidate
  exact validateFunctionYulIdentifiers_locals_ok_of_mem hyul hmem

theorem validateFunctionIdentifiers_assignTargets_avoidReservedCompilerPrefix
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateFunctionIdentifiers fn = Except.ok ())
    (hmem : name ∈ collectStmtListAssignedNames fn.body) :
    ¬ name.startsWith "__" := by
  have hyul :
      validateFunctionYulIdentifiers fn = Except.ok () := by
    unfold validateFunctionIdentifiers at hvalidate
    cases hname : ensureContractIdentifier "function" fn.name with
    | error err =>
        simp [hname] at hvalidate
        cases hvalidate
    | ok _ =>
        simpa [validateFunctionIdentifiers, hname] using hvalidate
  exact validateFunctionYulIdentifiers_assignTargets_ok_of_mem hyul hmem

theorem validateIdentifierShapes_field_avoidReservedCompilerPrefix
    {spec : CompilationModel}
    {field : Field}
    (hvalidate : validateIdentifierShapes spec = Except.ok ())
    (hmem : field ∈ spec.fields) :
    ¬ (field.name.startsWith "__" && !isInternalImmutableStorageName field.name) := by
  have hreserved :
      validateReservedCompilerIdentifiers spec = Except.ok () := by
    unfold validateIdentifierShapes at hvalidate
    cases hcontract : ensureContractIdentifier "contract" spec.name with
    | error err =>
        simp [hcontract] at hvalidate
        cases hvalidate
    | ok _ =>
        cases hreserved : validateReservedCompilerIdentifiers spec with
        | error err =>
            simp [hcontract, hreserved] at hvalidate
            cases hvalidate
        | ok a =>
            cases a
            rfl
  have hfields :
      validateFieldIdentifiers spec.fields = Except.ok () := by
    unfold validateReservedCompilerIdentifiers at hreserved
    cases hfields : validateFieldIdentifiers spec.fields with
    | error err =>
        simp [hfields] at hreserved
        cases hreserved
    | ok a =>
        cases a
        rfl
  exact validateFieldIdentifiers_ok_of_mem hfields hmem

theorem validateIdentifierShapes_functionIdentifiers_ok
    {spec : CompilationModel}
    {fn : FunctionSpec}
    (hvalidate : validateIdentifierShapes spec = Except.ok ())
    (hmem : fn ∈ spec.functions) :
    validateFunctionIdentifiers fn = Except.ok () := by
  have hreserved :
      validateReservedCompilerIdentifiers spec = Except.ok () := by
    unfold validateIdentifierShapes at hvalidate
    cases hcontract : ensureContractIdentifier "contract" spec.name with
    | error err =>
        simp [hcontract] at hvalidate
        cases hvalidate
    | ok _ =>
        cases hreserved : validateReservedCompilerIdentifiers spec with
        | error err =>
            simp [hcontract, hreserved] at hvalidate
            cases hvalidate
        | ok a =>
            cases a
            rfl
  have hfunctions :
      validateFunctionIdentifierList spec.functions = Except.ok () := by
    unfold validateReservedCompilerIdentifiers at hreserved
    cases hfields : validateFieldIdentifiers spec.fields with
    | error err =>
        simp [hfields] at hreserved
        cases hreserved
    | ok _ =>
        cases hfunctions : validateFunctionIdentifierList spec.functions with
        | error err =>
            simp [hfields, hfunctions] at hreserved
            cases hreserved
        | ok a =>
            cases a
            rfl
  exact validateFunctionIdentifierList_ok_of_mem hfunctions hmem

theorem validateIdentifierShapes_functionParams_avoidReservedCompilerPrefix
    {spec : CompilationModel}
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateIdentifierShapes spec = Except.ok ())
    (hfn : fn ∈ spec.functions)
    (hmem : name ∈ fn.params.map (·.name)) :
    ¬ name.startsWith "__" := by
  exact validateFunctionIdentifiers_params_avoidReservedCompilerPrefix
    (validateIdentifierShapes_functionIdentifiers_ok hvalidate hfn)
    hmem

theorem validateIdentifierShapes_functionLocals_avoidReservedCompilerPrefix
    {spec : CompilationModel}
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateIdentifierShapes spec = Except.ok ())
    (hfn : fn ∈ spec.functions)
    (hmem : name ∈ collectStmtListBindNames fn.body) :
    ¬ name.startsWith "__" := by
  exact validateFunctionIdentifiers_locals_avoidReservedCompilerPrefix
    (validateIdentifierShapes_functionIdentifiers_ok hvalidate hfn)
    hmem

theorem validateIdentifierShapes_functionAssignTargets_avoidReservedCompilerPrefix
    {spec : CompilationModel}
    {fn : FunctionSpec}
    {name : String}
    (hvalidate : validateIdentifierShapes spec = Except.ok ())
    (hfn : fn ∈ spec.functions)
    (hmem : name ∈ collectStmtListAssignedNames fn.body) :
    ¬ name.startsWith "__" := by
  exact validateFunctionIdentifiers_assignTargets_avoidReservedCompilerPrefix
    (validateIdentifierShapes_functionIdentifiers_ok hvalidate hfn)
    hmem

end Compiler.CompilationModel
