import Compiler.CompileDriver
import Compiler.ModuleInput
import Compiler.ParityPacks
import Verity.Core.Intrinsics

/-!
## CLI Argument Parsing

Supports:
- `--link <path>` : Link external Yul library (can be specified multiple times)
- `--output <dir>` or `-o <dir>` : Output directory (default: "artifacts/yul")
- `--verbose` or `-v` : Verbose output
- `--help` or `-h` : Show help message
-/

private structure CLIArgs where
  outDir : String := "artifacts/yul"
  abiOutDir : Option String := none
  manifestPath : Option String := none
  modules : List String := []
  libs : List String := []
  verbose : Bool := false
  backendProfile : Compiler.BackendProfile := .semantic
  backendProfileExplicit : Bool := false
  parityPackId : Option String := none
  targetFork : Verity.Core.Intrinsics.HardFork := .cancun
  targetForkExplicit : Bool := false
  allowFutureForkIntrinsics : Bool := false
  patchEnabled : Bool := false
  patchMaxIterations : Nat := 2
  patchMaxIterationsExplicit : Bool := false
  patchReportPath : Option String := none
  trustReportPath : Option String := none
  assumptionReportPath : Option String := none
  layoutReportPath : Option String := none
  layoutCompatibilityReportPath : Option String := none
  denyUncheckedDependencies : Bool := false
  denyAssumedDependencies : Bool := false
  denyAxiomatizedPrimitives : Bool := false
  denyLocalObligations : Bool := false
  denyLinearMemoryMechanics : Bool := false
  denyEventEmission : Bool := false
  denyLowLevelMechanics : Bool := false
  denyRuntimeIntrospection : Bool := false
  denyProxyUpgradeability : Bool := false
  denyLayoutIncompatibility : Bool := false
  mappingSlotScratchBase : Nat := 0
  mappingSlotScratchBaseExplicit : Bool := false

private def profileForcesPatches (profile : Compiler.BackendProfile) : Bool :=
  match profile with
  | .solidityParity => true
  | _ => false

private def packForcesPatches (cfg : CLIArgs) : Bool :=
  match cfg.parityPackId with
  | some packId =>
      match Compiler.findParityPack? packId with
      | some pack => pack.forcePatches
      | none => false
  | none => false

private def patchEnabledFor (cfg : CLIArgs) : Bool :=
  cfg.patchEnabled || profileForcesPatches cfg.backendProfile || packForcesPatches cfg

private def defaultRewriteBundleIdFor (cfg : CLIArgs) : String :=
  match cfg.parityPackId with
  | some packId =>
      match Compiler.findParityPack? packId with
      | some pack => pack.rewriteBundleId
      | none => Compiler.Yul.foundationRewriteBundleId
  | none => Compiler.Yul.foundationRewriteBundleId

private def requiredProofRefsFor (cfg : CLIArgs) : List Lean.Name :=
  match cfg.parityPackId with
  | some packId =>
      match Compiler.findParityPack? packId with
      | some pack => pack.requiredProofRefs
      | none => Compiler.Yul.foundationProofAllowlist
  | none =>
      Compiler.Yul.rewriteProofAllowlistForId (defaultRewriteBundleIdFor cfg)

example :
    defaultRewriteBundleIdFor
      { backendProfile := .solidityParity
        patchEnabled := true } =
      Compiler.Yul.foundationRewriteBundleId := by
  native_decide

example :
    requiredProofRefsFor
      { backendProfile := .solidityParity
        patchEnabled := true } =
      Compiler.Yul.foundationProofAllowlist := by
  native_decide

private def parseBackendProfile (raw : String) : Option Compiler.BackendProfile :=
  match raw with
  | "semantic" => some .semantic
  | "solidity-parity-ordering" => some .solidityParityOrdering
  | "solidity-parity" => some .solidityParity
  | _ => none

private def backendProfileString (profile : Compiler.BackendProfile) : String :=
  match profile with
  | .semantic => "semantic"
  | .solidityParityOrdering => "solidity-parity-ordering"
  | .solidityParity => "solidity-parity"

private def parseTargetFork (raw : String) : Option Verity.Core.Intrinsics.HardFork :=
  Verity.Core.Intrinsics.HardFork.parse? raw

private def parseTomlStringValue? (line : String) : Option String :=
  match line.splitOn "=" with
  | _ :: rhsParts =>
      let rhs := String.intercalate "=" rhsParts
      let value := (rhs.splitOn "#").head!.trim
      match value.splitOn "\"" with
      | _ :: quoted :: _ => some quoted.trim
      | _ => some value
  | _ => none

private def tamaTomlTargetFork? : IO (Option Verity.Core.Intrinsics.HardFork) := do
  try
    let text ← IO.FS.readFile "tama.toml"
    let rec scan (inYul : Bool) : List String → Option String
      | [] => none
      | line :: rest =>
          let trimmed := line.trim
          if trimmed.startsWith "[" then
            scan (trimmed == "[yul]") rest
          else if inYul && trimmed.startsWith "evm_version" then
            parseTomlStringValue? trimmed
          else
            scan inYul rest
    pure <| (scan false (text.splitOn "\n")).bind parseTargetFork
  catch _ =>
    pure none

private def parseArgs (args : List String) : IO CLIArgs := do
  let rec go (remaining : List String) (cfg : CLIArgs) : IO CLIArgs :=
    match remaining with
    | [] => pure { cfg with libs := cfg.libs.reverse, modules := cfg.modules.reverse }
    | "--help" :: _ | "-h" :: _ => do
        IO.println "Verity Compiler (patched)"
        IO.println ""
        IO.println "Usage: verity-compiler-patched [options]"
        IO.println ""
        IO.println "Options:"
        IO.println "  --link <path>      Link external Yul library (can be used multiple times)"
        IO.println "  --output <dir>     Output directory (default: artifacts/yul)"
        IO.println "  -o <dir>           Short form of --output"
        IO.println "  --abi-output <dir> Output ABI JSON artifacts (one <Contract>.abi.json per spec)"
        IO.println "  --manifest <path>  Manifest file with one Lean module per line"
        IO.println "  --module <name>    Import a Lean module and compile its canonical `<Module>.spec`"
        IO.println "  --backend-profile <semantic|solidity-parity-ordering|solidity-parity>"
        IO.println "  --parity-pack <id> Versioned parity-pack tuple (see docs/PARITY_PACKS.md)"
        IO.println "  --target-fork <cancun|prague|fusaka|osaka>  EVM fork target for intrinsic min_fork checks (default: cancun)"
        IO.println "  --allow-future-fork-intrinsics  Allow intrinsics whose min_fork is newer than --target-fork"
        IO.println "  --enable-patches   Enable deterministic Yul patch pass"
        IO.println "  --patch-max-iterations <n>  Max patch-pass fixpoint iterations (default: 2)"
        IO.println "  --patch-report <path>       Write TSV patch coverage report"
        IO.println "  --trust-report <path>       Write JSON trust-surface report"
        IO.println "  --assumption-report <path>  Write JSON assumption inventory report"
        IO.println "  --layout-report <path>      Write JSON storage-layout report"
        IO.println "  --layout-compat-report <path>  Compare baseline/candidate layouts and write JSON compatibility report"
        IO.println "  --deny-unchecked-dependencies  Fail if any contract depends on `unchecked` foreign surfaces"
        IO.println "  --deny-assumed-dependencies    Fail if any contract depends on `assumed` or `unchecked` foreign surfaces"
        IO.println "  --deny-axiomatized-primitives  Fail if any contract uses axiomatized primitives"
        IO.println "  --deny-local-obligations      Fail if any contract keeps undischarged local unsafe/refinement obligations"
        IO.println "  --deny-linear-memory-mechanics  Fail if any contract uses partially modeled linear-memory mechanics"
        IO.println "  --deny-event-emission         Fail if any contract uses raw `rawLog` event emission"
        IO.println "  --deny-low-level-mechanics    Fail if any contract uses first-class low-level call / returndata mechanics"
        IO.println "  --deny-runtime-introspection   Fail if any contract uses partially modeled runtime-introspection primitives"
        IO.println "  --deny-proxy-upgradeability   Fail if any contract uses `delegatecall`-style proxy / upgradeability mechanics"
        IO.println "  --deny-layout-incompatibility Fail if the candidate layout moves or mutates baseline storage fields"
        IO.println "  --mapping-slot-scratch-base <n>  Scratch memory base for mappingSlot helper (default: 0)"
        IO.println "  --verbose          Enable verbose output"
        IO.println "  -v                 Short form of --verbose"
        IO.println "  --help             Show this help message"
        IO.println "  -h                 Short form of --help"
        IO.println ""
        IO.println "Example:"
        IO.println "  verity-compiler-patched --manifest packages/verity-examples/contracts.manifest -o artifacts/yul"
        IO.println "  verity-compiler-patched --module Contracts.Counter.Counter -o artifacts/yul"
        IO.println "  verity-compiler-patched --enable-patches --patch-report artifacts/patch-report.tsv"
        throw (IO.userError "help")
    | "--link" :: path :: rest =>
        go rest { cfg with libs := path :: cfg.libs }
    | ["--link"] =>
        throw (IO.userError "Missing value for --link")
    | "--output" :: dir :: rest | "-o" :: dir :: rest =>
        go rest { cfg with outDir := dir }
    | ["--output"] | ["-o"] =>
        throw (IO.userError "Missing value for --output")
    | "--abi-output" :: dir :: rest =>
        go rest { cfg with abiOutDir := some dir }
    | ["--abi-output"] =>
        throw (IO.userError "Missing value for --abi-output")
    | "--manifest" :: path :: rest =>
        if cfg.manifestPath.isSome then
          throw (IO.userError "Cannot specify --manifest more than once")
        else
          go rest { cfg with manifestPath := some path }
    | ["--manifest"] =>
        throw (IO.userError "Missing value for --manifest")
    | "--module" :: raw :: rest =>
        go rest { cfg with modules := raw :: cfg.modules }
    | ["--module"] =>
        throw (IO.userError "Missing value for --module")
    | "--backend-profile" :: raw :: rest =>
        if cfg.parityPackId.isSome then
          throw (IO.userError "Cannot combine --backend-profile with --parity-pack")
        else
          match parseBackendProfile raw with
          | some profile => go rest { cfg with backendProfile := profile, backendProfileExplicit := true }
          | none =>
              throw (IO.userError s!"Invalid value for --backend-profile: {raw} (expected semantic, solidity-parity-ordering, or solidity-parity)")
    | ["--backend-profile"] =>
        throw (IO.userError "Missing value for --backend-profile")
    | "--parity-pack" :: raw :: rest =>
        if cfg.parityPackId.isSome then
          throw (IO.userError "Cannot specify --parity-pack more than once")
        else if cfg.backendProfileExplicit then
          throw (IO.userError "Cannot combine --parity-pack with --backend-profile")
        else
          match Compiler.findParityPack? raw with
          | some pack =>
              if !pack.proofCompositionValid then
                throw (IO.userError
                  s!"Parity pack '{pack.id}' is missing valid proof composition metadata")
              else
                go rest {
                  cfg with
                    parityPackId := some pack.id
                    backendProfile := pack.backendProfile
                    patchEnabled := cfg.patchEnabled || pack.forcePatches
                    patchMaxIterations :=
                      if cfg.patchMaxIterationsExplicit then cfg.patchMaxIterations else pack.defaultPatchMaxIterations
                    targetFork :=
                      if cfg.targetForkExplicit then cfg.targetFork
                      else (Verity.Core.Intrinsics.HardFork.parse? pack.compat.evmVersion).getD cfg.targetFork
                    mappingSlotScratchBase :=
                      if cfg.mappingSlotScratchBaseExplicit then cfg.mappingSlotScratchBase else 0x200
               }
          | none =>
              throw (IO.userError
                s!"Invalid value for --parity-pack: {raw} (supported: {String.intercalate ", " Compiler.supportedParityPackIds})")
    | ["--parity-pack"] =>
        throw (IO.userError "Missing value for --parity-pack")
    | "--target-fork" :: raw :: rest =>
        match parseTargetFork raw with
        | some fork => go rest { cfg with targetFork := fork, targetForkExplicit := true }
        | none =>
            throw (IO.userError
              s!"Invalid value for --target-fork: {raw} (expected cancun, prague, fusaka, or osaka alias)")
    | ["--target-fork"] =>
        throw (IO.userError "Missing value for --target-fork")
    | "--allow-future-fork-intrinsics" :: rest =>
        go rest { cfg with allowFutureForkIntrinsics := true }
    | "--enable-patches" :: rest =>
        go rest { cfg with patchEnabled := true }
    | "--patch-max-iterations" :: raw :: rest =>
        match raw.toNat? with
        | some n => go rest { cfg with patchEnabled := true, patchMaxIterations := n, patchMaxIterationsExplicit := true }
        | none => throw (IO.userError s!"Invalid value for --patch-max-iterations: {raw}")
    | ["--patch-max-iterations"] =>
        throw (IO.userError "Missing value for --patch-max-iterations")
    | "--patch-report" :: path :: rest =>
        go rest { cfg with patchEnabled := true, patchReportPath := some path }
    | ["--patch-report"] =>
        throw (IO.userError "Missing value for --patch-report")
    | "--trust-report" :: path :: rest =>
        go rest { cfg with trustReportPath := some path }
    | ["--trust-report"] =>
        throw (IO.userError "Missing value for --trust-report")
    | "--assumption-report" :: path :: rest =>
        go rest { cfg with assumptionReportPath := some path }
    | ["--assumption-report"] =>
        throw (IO.userError "Missing value for --assumption-report")
    | "--layout-report" :: path :: rest =>
        go rest { cfg with layoutReportPath := some path }
    | ["--layout-report"] =>
        throw (IO.userError "Missing value for --layout-report")
    | "--layout-compat-report" :: path :: rest =>
        go rest { cfg with layoutCompatibilityReportPath := some path }
    | ["--layout-compat-report"] =>
        throw (IO.userError "Missing value for --layout-compat-report")
    | "--deny-unchecked-dependencies" :: rest =>
        go rest { cfg with denyUncheckedDependencies := true }
    | "--deny-assumed-dependencies" :: rest =>
        go rest { cfg with denyAssumedDependencies := true }
    | "--deny-axiomatized-primitives" :: rest =>
        go rest { cfg with denyAxiomatizedPrimitives := true }
    | "--deny-local-obligations" :: rest =>
        go rest { cfg with denyLocalObligations := true }
    | "--deny-linear-memory-mechanics" :: rest =>
        go rest { cfg with denyLinearMemoryMechanics := true }
    | "--deny-event-emission" :: rest =>
        go rest { cfg with denyEventEmission := true }
    | "--deny-low-level-mechanics" :: rest =>
        go rest { cfg with denyLowLevelMechanics := true }
    | "--deny-runtime-introspection" :: rest =>
        go rest { cfg with denyRuntimeIntrospection := true }
    | "--deny-proxy-upgradeability" :: rest =>
        go rest { cfg with denyProxyUpgradeability := true }
    | "--deny-layout-incompatibility" :: rest =>
        go rest { cfg with denyLayoutIncompatibility := true }
    | "--mapping-slot-scratch-base" :: raw :: rest =>
        match raw.toNat? with
        | some n => go rest { cfg with mappingSlotScratchBase := n, mappingSlotScratchBaseExplicit := true }
        | none => throw (IO.userError s!"Invalid value for --mapping-slot-scratch-base: {raw}")
    | ["--mapping-slot-scratch-base"] =>
        throw (IO.userError "Missing value for --mapping-slot-scratch-base")
    | "--verbose" :: rest | "-v" :: rest =>
        go rest { cfg with verbose := true }
    | unknown :: _ =>
        throw (IO.userError s!"Unknown argument: {unknown}\nUse --help for usage information")
  go args {}

unsafe def main (args : List String) : IO Unit := do
  try
    let parsedCfg ← parseArgs args
    let cfg ←
      if parsedCfg.targetForkExplicit then
        pure parsedCfg
      else
        match ← tamaTomlTargetFork? with
        | some fork => pure { parsedCfg with targetFork := fork }
        | none => pure parsedCfg
    let rawModules ←
      match ← Compiler.ModuleInput.resolveRawModules cfg.manifestPath cfg.modules with
      | .ok modules => pure modules
      | .error err => throw (IO.userError err)
    if rawModules.isEmpty then
      throw (IO.userError "No compiler input provided. Use --manifest and/or --module.")
    let patchEnabled := patchEnabledFor cfg
    if cfg.verbose then
      IO.println s!"Output directory: {cfg.outDir}"
      IO.println "Input mode: manifest/modules"
      match cfg.manifestPath with
      | some path => IO.println s!"Manifest: {path}"
      | none => pure ()
      if !rawModules.isEmpty then
        IO.println s!"Modules: {String.intercalate ", " rawModules}"
      IO.println s!"Backend profile: {backendProfileString cfg.backendProfile}"
      IO.println s!"Target fork: {cfg.targetFork}"
      if cfg.allowFutureForkIntrinsics then
        IO.println "Future-fork intrinsics: allowed"
      match cfg.parityPackId with
      | some packId =>
          IO.println s!"Parity pack: {packId}"
          match Compiler.findParityPack? packId with
          | some pack =>
              IO.println s!"  target solc: {pack.compat.solcVersion}+commit.{pack.compat.solcCommit}"
              IO.println s!"  optimizer runs: {pack.compat.optimizerRuns}"
              IO.println s!"  viaIR: {pack.compat.viaIR}"
              IO.println s!"  evmVersion: {pack.compat.evmVersion}"
              IO.println s!"  metadataMode: {pack.compat.metadataMode}"
              IO.println s!"  rewriteBundle: {pack.rewriteBundleId}"
          | none => pure ()
      | none => pure ()
      match cfg.abiOutDir with
      | some dir => IO.println s!"ABI output directory: {dir}"
      | none => pure ()
      if patchEnabled then
        IO.println s!"Patch pass: enabled (max iterations = {cfg.patchMaxIterations})"
      if !cfg.libs.isEmpty then
        IO.println s!"External libraries: {cfg.libs.length}"
        for lib in cfg.libs do
          IO.println s!"  - {lib}"
      match cfg.patchReportPath with
      | some path => IO.println s!"Patch report: {path}"
      | none => pure ()
      match cfg.trustReportPath with
      | some path => IO.println s!"Trust report: {path}"
      | none => pure ()
      match cfg.assumptionReportPath with
      | some path => IO.println s!"Assumption report: {path}"
      | none => pure ()
      match cfg.layoutReportPath with
      | some path => IO.println s!"Layout report: {path}"
      | none => pure ()
      match cfg.layoutCompatibilityReportPath with
      | some path => IO.println s!"Layout compatibility report: {path}"
      | none => pure ()
      if cfg.denyLinearMemoryMechanics then
        IO.println "Linear memory mechanics: denied"
      if cfg.denyEventEmission then
        IO.println "Event emission: denied"
      if cfg.denyLowLevelMechanics then
        IO.println "Low-level mechanics: denied"
      if cfg.denyRuntimeIntrospection then
        IO.println "Runtime introspection: denied"
      if cfg.denyProxyUpgradeability then
        IO.println "Proxy / upgradeability: denied"
      if cfg.denyLayoutIncompatibility then
        IO.println "Layout incompatibility: denied"
      if cfg.denyAssumedDependencies then
        IO.println "Assumed dependencies: denied"
      if cfg.denyAxiomatizedPrimitives then
        IO.println "Axiomatized primitives: denied"
      if cfg.denyUncheckedDependencies then
        IO.println "Unchecked dependencies: denied"
      IO.println s!"Mapping slot scratch base: {cfg.mappingSlotScratchBase}"
      IO.println ""
    let packRequiredProofRefs := requiredProofRefsFor cfg
    let packRewriteBundleId := defaultRewriteBundleIdFor cfg
    let options : Compiler.YulEmitOptions := {
      backendProfile := cfg.backendProfile
      targetFork := cfg.targetFork
      allowFutureForkIntrinsics := cfg.allowFutureForkIntrinsics
      patchConfig := {
        enabled := patchEnabled
        maxIterations := cfg.patchMaxIterations
        packId := cfg.parityPackId.getD ""
        rewriteBundleId := packRewriteBundleId
        requiredProofRefs := packRequiredProofRefs
      }
      mappingSlotScratchBase := cfg.mappingSlotScratchBase
    }
    Compiler.compileModulesWithOptions
      cfg.outDir rawModules cfg.verbose cfg.libs options cfg.patchReportPath cfg.trustReportPath
      cfg.assumptionReportPath cfg.abiOutDir cfg.denyUncheckedDependencies cfg.denyAssumedDependencies
      cfg.denyAxiomatizedPrimitives cfg.denyLocalObligations cfg.denyLinearMemoryMechanics cfg.denyEventEmission
      cfg.denyLowLevelMechanics cfg.denyRuntimeIntrospection cfg.denyProxyUpgradeability cfg.layoutReportPath
      cfg.layoutCompatibilityReportPath cfg.denyLayoutIncompatibility
  catch e =>
    if e.toString == "help" then
      -- Help was shown, exit cleanly
      return ()
    else
      throw e
